# Maintainer: none

pkgname=glibc
pkgver=2.35
pkgrel=8.2
pkgdesc='GNU C Library'
arch=('x86_64')
url='https://www.gnu.org/software/libc'
license=('GPL' 'LGPL')
depends=('linux-api-headers' 'tzdata')
makedepends=('git' 'gd' 'python')
optdepends=('perl: for mtrace'
            'gd: graph image generation with memusage')
backup=(etc/gai.conf
        etc/locale.gen
        etc/nscd.conf)
options=('!strip' 'staticlibs' '!lto')
install=glibc.install
source=("git+https://sourceware.org/git/glibc.git#branch=release/$pkgver/master"
#source=("git+https://sourceware.org/git/glibc.git"
        locale.gen.txt
        locale-gen
        0001-Force-ffsll-to-be-64-bytes-aligned.patch
        0001-Set-vector-width-and-alignment-to-fix-GCC-AVX-issue.patch
        disable-vectorization-even-more.patch
        malloc_tune.patch
        mathlto.patch
        nostackshrink.patch
        spin-smarter.patch
        tzselect-proper-zone-file.patch
        vzeroupper-2.27.patch
        alternate_trim.patch
        nonscd.patch
        populate.patch
        01-mandriva-no-__builtin_va_arg_pack-with-clang.patch
        02-mandriva-float64.patch
        03-mandriva-morefloat.patch
        04-mandriva-clangheader.patch
        05-mandriva-zstdcompressedlocals.patch
        06-mandriva-nss-crash.patch
        iolocks.patch)
sha256sums=('SKIP'
            'd42648cea552ba5353a32e264686e992263289d5cc86207314dffc54ab514981'
            '83f108f915863c7ed0338e2d3e8f2e071a531a090ef8f8b2eb3a956a3c4f04d7')

prepare() {

    mkdir -p glibc-build

    local src
    for src in "${source[@]}"; do
        src="${src%%::*}"
        src="${src##*/}"
        [[ $src = *.patch ]] || continue
        echo "Applying patch $src..."
        patch --directory="glibc" --forward --strip=1 < "../$src"
    done
}

build() {
  cd glibc-build

  echo "slibdir=/usr/lib" >> configparms
  echo "rtlddir=/usr/lib" >> configparms
  echo "sbindir=/usr/bin" >> configparms
  echo "rootsbindir=/usr/bin" >> configparms

  # remove fortify for building libraries
  CFLAGS=${CFLAGS/-Wp,-D_FORTIFY_SOURCE=2/}

  "$srcdir/glibc/configure" \
      --prefix=/usr \
      --libdir=/usr/lib \
      --libexecdir=/usr/lib \
      --with-headers=/usr/include \
      --enable-bind-now \
      --disable-cet \
      --enable-kernel=5.17 \
      --enable-multi-arch \
      --enable-stack-protector=strong \
      --disable-crypt \
      --disable-werror

  echo "build-programs=no" >> configparms
  make -O

  # re-enable fortify for programs
  sed -i "/build-programs=/s#no#yes#" configparms
  echo "CFLAGS += -D_FORTIFY_SOURCE=2" >> configparms
  make -O

  # build info pages manually for reproducibility
  make info
}

skip_test() {
  test=$1
  file=$2
  sed -i "s/\b$test\b//" $srcdir/glibc/$file
}

package() {
  make -C glibc-build install_root="$pkgdir" install

  # this is generated in post-install
  rm -f "$pkgdir"/etc/ld.so.cache

  # shipped in tzdata
  rm -f "$pkgdir"/usr/bin/{tzselect,zdump,zic}

  install -dm755 "$pkgdir"/usr/lib/{locale,systemd/system,tmpfiles.d}

  install -m644 glibc/nscd/nscd.conf "$pkgdir"/etc/nscd.conf
  install -m644 glibc/nscd/nscd.tmpfiles "$pkgdir"/usr/lib/tmpfiles.d/nscd.conf
  install -m644 glibc/nscd/nscd.service "$pkgdir"/usr/lib/systemd/system/nscd.service

  install -m644 glibc/posix/gai.conf "$pkgdir"/etc/gai.conf

  install -m755 locale-gen "$pkgdir"/usr/bin/locale-gen

  # create /etc/locale.gen
  install -m644 "$srcdir"/locale.gen.txt "$pkgdir"/etc/locale.gen
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
    glibc/localedata/SUPPORTED >> "$pkgdir"/etc/locale.gen

  if check_option 'debug' n; then
    find "$pkgdir"/usr/bin -type f -executable -exec strip $STRIP_BINARIES {} + 2> /dev/null || true
    find "$pkgdir"/usr/lib -name '*.a' -type f -exec strip $STRIP_STATIC {} + 2> /dev/null || true

    # do not strip libraries that have symbols that are useful for gdb and valgrind functionality
    find "$pkgdir"/usr/lib \
      -not -name 'ld-*.so' \
      -not -name 'libc-*.so' \
      -not -name 'libpthread-*.so' \
      -not -name 'libthread_db-*.so' \
      -name '*-*.so' -type f -exec strip $STRIP_SHARED {} + 2> /dev/null || true
  fi
}
