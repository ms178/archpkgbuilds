# toolchain build order: linux-api-headers->glibc->binutils->gcc->binutils->glibc
# Maintainer: None


pkgname=binutils
pkgver=2.38
pkgrel=7.0
pkgdesc='A set of programs to assemble and manipulate binary and object files (git master developmental version)'
arch=(x86_64)
url='http://www.gnu.org/software/binutils/'
license=(GPL)
depends=(glibc zlib libelf)
makedepends=(git)
checkdepends=(dejagnu bc)
provides=("binutils=${pkgver}")
conflicts=(binutils-multilib binutils)
options=(staticlibs !distcc !ccache)
source=("git+https://sourceware.org/git/binutils-gdb.git#branch=binutils-${pkgver/./_}-branch")
#source=(git+https://sourceware.org/git/binutils-gdb.git)
sha256sums=('SKIP')

prepare() {
  mkdir -p binutils-build

  cd binutils-build

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  #sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure
}

build() {
  cd binutils-build

  "$srcdir/binutils-gdb/configure" \
    --prefix=/usr \
    --with-lib-path=/usr/lib:/usr/local/lib \
    --enable-deterministic-archives \
    --enable-gold \
    --enable-ld=default \
    --enable-pgo-build=lto \
    --enable-plugins \
    --enable-64-bit-bfd \
    --enable-shared \
    --enable-threads \
    --disable-cet \
    --disable-checking \
    --disable-gdb \
    --disable-gdbserver \
    --disable-libdecnumber \
    --disable-readline \
    --disable-sim \
    --disable-werror \
    --with-system-zlib

  make configure-host
  make tooldir=/usr
}

package() {
  cd binutils-build
  make prefix="$pkgdir/usr" tooldir="$pkgdir/usr" install

  # Remove unwanted files
  rm -f "$pkgdir"/usr/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/usr/lib/lib{bfd,opcodes}.so
  echo 'INPUT( /usr/lib/libbfd.a -liberty -lz -ldl )' > "$pkgdir/usr/lib/libbfd.so"
  echo 'INPUT( /usr/lib/libopcodes.a -lbfd )' > "$pkgdir/usr/lib/libopcodes.so"
}
