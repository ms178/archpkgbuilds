# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=mingw-w64-binutils
pkgver=2.40
pkgrel=4.1
#_commit=165accf07523db070eb25866a614b9a8f9c8e281
pkgdesc="Cross binutils for the MinGW-w64 cross-compiler"
arch=('x86_64')
url="http://www.gnu.org/software/binutils"
license=('GPL')
groups=('mingw-w64-toolchain' 'mingw-w64')
depends=('zlib')
options=('!libtool' '!emptydirs')
validpgpkeys=('3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F')  # Nick Clifton (Chief Binutils Maintainer) <nickc@redhat.com>
source=(git+https://sourceware.org/git/binutils-gdb.git
        gold-warn-unsupported.patch compilespeed.patch zlib-level.patch)
sha512sums=('5160ff5c2a0ad41b880384216e21ada5328b51378e760c04269eb3b3626400d12abeeea72c14e18b52b969a0be94c8e5285c0b568bd5c82b7e69f68e0dfdc743'
            'SKIP'
            'cdce2ac2666fe27973152748a0108475bd1c0bbfa1559116d42c6badcdb0a60258b2a01180e0b37a65bef8df520a795d105eb80d88ce1846d006b5ea818b2c60')

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  [[ ! -d binutils-gdb ]] && ln -s binutils-${pkgver} binutils-gdb
  mkdir -p binutils-build

  cd binutils-gdb
  patch -Np1 -i "${srcdir}"/gold-warn-unsupported.patch
  patch -Np1 -i "${srcdir}"/compilespeed.patch
  patch -Np1 -i "${srcdir}"/zlib-level.patch
  #do not install libiberty
  sed -i 's/install_to_$(INSTALL_DEST) //' libiberty/Makefile.in
}

build() {
  for _target in $_targets; do
    echo "Building ${_target} cross binutils"
    mkdir -p "$srcdir"/binutils-${_target} && cd "${srcdir}/binutils-${_target}"
    "$srcdir"/binutils-gdb/configure --prefix=/usr \
        --target=${_target} \
        --infodir=/usr/share/info/${_target} \
        --enable-lto --enable-plugins \
        --enable-deterministic-archives \
        --disable-cet \
        --disable-jansson \
        --disable-gdb \
        --disable-gdbserver \
        --enable-threads \
        --enable-colored-disassembly \
        --disable-multilib --disable-nls \
        --with-system-zlib \
        --enable-new-dtags \
        --disable-relro \
        --disable-werror
     make -O
  done
}

package() {
  for _target in ${_targets}; do
    echo "Installing ${_target} cross binutils"
    cd "$srcdir"/binutils-${_target}
    make DESTDIR="$pkgdir" install

    rm "$pkgdir"/usr/lib/bfd-plugins/libdep.so
  done
}
