# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=sddm-kcm
pkgver=5.27.10
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=4.1
pkgdesc='KDE Config Module for SDDM'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(GPL)
depends=(sddm knewstuff5 systemsettings)
makedepends=(extra-cmake-modules kdoctools5)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('83e43177bf83d7b2c5d617349bb913dbdb80b0354aecbcc47febd4e50d95f4ad'
            'SKIP')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release  \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
