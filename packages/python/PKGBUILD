shopt -s extglob

pkgbase=python
pkgname=(python python-tests)
pkgver=3.14.2
pkgrel=4.1
_pybasever=${pkgver%.*}
pkgdesc="The Python programming language"
arch=('x86_64')
license=('PSF-2.0')
url="https://www.python.org/"
options=(!lto)

depends=(
  bzip2 expat gdbm libffi libnsl libxcrypt openssl zlib mpdecimal
  ncurses readline sqlite xz
  tk bluez-libs
  tzdata
)
makedepends=(
  clang llvm lld
  pkgconf
  xorg-server-xvfb ttf-dejavu
  gdb
)

source=(
  "https://www.python.org/ftp/python/${pkgver%rc*}/Python-${pkgver}.tar.xz"
  EXTERNALLY-MANAGED
)
sha512sums=(
  '056c9b5fc0a6b540f41513d045f43c1ed463d15e0f345cecec703ec9c2335e53b4beb19de9c74ab2b236b023f934d5fd9ae7727a808634eaa01cfe66018a9a35'
  '62a6fbfbaeaa3ba7c54e109d9c3b7f67e73bb21986da4c1fcc5d28cca83d71e0fcae28e1fc70ee8ddce7dea8cd0b64e18d1031dae3a2eae5eaa379c53efd53a0'
)

validpgpkeys=(
  '0D96DF4D4110E5C43FBFB17F2D347EA6AA65421D'
  'E3FF2839C048B25C084DEBE9B26995E310250568'
  'A035C8C19219BA821ECEA86B64E628F8D684696D'
  '7169605F62C751356D054A26A821E680E5FA6305'
)

prepare() {
  cd "Python-${pkgver}"
  rm -rf Modules/expat
  rm -rf Modules/_decimal/libmpdec
  rm -rf Modules/_ctypes/libffi 2>/dev/null || true
}

build() {
  cd "Python-${pkgver}"

  export CC=clang
  export CXX=clang++
  export AR=llvm-ar
  export NM=llvm-nm
  export RANLIB=llvm-ranlib
  export LDFLAGS+=" -fuse-ld=lld"

  export CFLAGS="${CFLAGS/-O2/-O3}"
  export CXXFLAGS="${CXXFLAGS/-O2/-O3}"
  export CFLAGS+=" -fno-semantic-interposition"
  export CXXFLAGS+=" -fno-semantic-interposition"

  local _real_profdata
  _real_profdata="$(${CC} --print-prog-name=llvm-profdata)"

  cat > "${srcdir}/llvm-profdata-pgo-wrapper" <<EOF
#!/bin/bash
set -euo pipefail
if [[ "\${1:-}" == "merge" ]]; then
  shift
  exec "$_real_profdata" merge --failure-mode=all "\$@"
fi
exec "$_real_profdata" "\$@"
EOF
  chmod +x "${srcdir}/llvm-profdata-pgo-wrapper"
  export LLVM_PROFDATA="${srcdir}/llvm-profdata-pgo-wrapper"

  mkdir -p build
  export LLVM_PROFILE_FILE="${PWD}/build/code-%p.profclangr"

  local _jobs
  _jobs="$(nproc)"
  local _profile_task="-m test --pgo-extended -j${_jobs} -x test_perf_profiler"

  cd build

  ../configure \
    --prefix=/usr \
    --enable-shared \
    --with-computed-gotos \
    --enable-optimizations \
    --with-lto=full \
    --enable-ipv6 \
    --with-system-expat \
    --with-system-libmpdec \
    --with-system-ffi \
    --with-dbmliborder=gdbm:ndbm \
    --enable-loadable-sqlite-extensions \
    --with-ensurepip=no \
    --with-tzpath=/usr/share/zoneinfo \
    --disable-bolt

  LC_ALL=C.UTF-8 \
  xvfb-run -a -s "-screen 0 1920x1080x24" \
    make profile-opt PROFILE_TASK="$_profile_task"
}

package_python() {
  provides=('python3' 'python-externally-managed')
  conflicts=('python3')
  optdepends=(
    'python-setuptools: build Python packages'
    'python-pip: install Python packages'
  )

  cd "Python-${pkgver}/build"
  make DESTDIR="${pkgdir}" install

  ln -sf python3 "${pkgdir}/usr/bin/python"
  ln -sf python3-config "${pkgdir}/usr/bin/python-config"
  ln -sf idle3 "${pkgdir}/usr/bin/idle"
  ln -sf pydoc3 "${pkgdir}/usr/bin/pydoc"
  ln -sf "python${_pybasever}.1" "${pkgdir}/usr/share/man/man1/python.1"

  cd "${srcdir}/Python-${pkgver}"
  install -dm755 "${pkgdir}/usr/lib/python${_pybasever}/Tools/"{i18n,scripts}
  install -m755 Tools/i18n/{msgfmt,pygettext}.py \
    "${pkgdir}/usr/lib/python${_pybasever}/Tools/i18n/"
  install -m755 Tools/scripts/*.py \
    "${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/"
  [[ -f Tools/scripts/README ]] && \
    install -m644 Tools/scripts/README \
      "${pkgdir}/usr/lib/python${_pybasever}/Tools/scripts/"

  install -Dm644 "${srcdir}/EXTERNALLY-MANAGED" \
    "${pkgdir}/usr/lib/python${_pybasever}/EXTERNALLY-MANAGED"

  cd "${pkgdir}/usr/lib/python${_pybasever}"
  rm -rf test idlelib/idle_test
}

package_python-tests() {
  pkgdesc="Regression tests package for Python"
  depends=('python')

  cd "Python-${pkgver}/build"
  make DESTDIR="${pkgdir}" libinstall

  cd "${pkgdir}/usr/lib/python${_pybasever}"
  rm -rf !(test)
}
