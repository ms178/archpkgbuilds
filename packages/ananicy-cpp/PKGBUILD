# Maintainer: dr460nf1r3 <dr460nf1r3 at garudalinux dot org>
# Maintainer: Antoine Viallon <antoine@lesviallon.fr>

pkgname=ananicy-cpp-git
_pkgname=ananicy-cpp
pkgver=1.0.0.rc7.r59.g93ba42a
pkgrel=1
pkgdesc="Ananicy Cpp is a full rewrite of Ananicy in C++, featuring lower CPU and RAM usage."
url="https://gitlab.com/ananicy-cpp/ananicy-cpp.git"
source=(
	"git+https://gitlab.com/ananicy-cpp/ananicy-cpp.git"
	"git+https://gitlab.com/vnepogodin/std-format.git"
)
md5sums=('SKIP'
         'SKIP')
arch=(x86_64 i386 armv7h)
depends=(fmt spdlog nlohmann-json systemd)
makedepends=(cmake git gcc)
conflicts=(ananicy-cpp)
provides=(ananicy-cpp)
optdepends=("ananicy-rules-git: community rules"
            "ananicy-rules: Rules based for ananicy-cpp")

pkgver() {
  cd "$_pkgname"
  git describe --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
	cd "$_pkgname"

	git submodule init
	git config submodule."external/std-format".url "${srcdir}/std-format"
	git -c protocol.file.allow=always submodule update

	cmake -B build -S . \
		-DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_C_STANDARD=23 \
		-DCMAKE_CXX_STANDARD=20 \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DUSE_EXTERNAL_SPDLOG=OFF \
		-DUSE_EXTERNAL_JSON=OFF \
		-DUSE_EXTERNAL_FMTLIB=OFF \
        -DENABLE_SYSTEMD=ON \
        -DOPTIMIZE_FOR_NATIVE_MICROARCH=ON \
        -DUSE_EXPERIMENTAL_IMPL=ON \
        -DUSE_BPF_PROC_IMPL=OFF \
        -DBPF_BUILD_LIBBPF=OFF \
        -DENABLE_IPO=ON
}

build() {
	cd "$_pkgname"
	cmake --build build --target ananicy-cpp
}

package() {
	cd "$_pkgname"
	export DESTDIR="$pkgdir"
	cmake --install build --component Runtime

	install -m755 -d "$pkgdir/etc/ananicy.d"
}
