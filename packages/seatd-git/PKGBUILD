# Maintainer: Kenny Levinsen <aur [at] kl [dot] wtf>

pkgname=seatd-git
pkgver=0.8.0.13.g5460222
pkgrel=1
pkgdesc="Seat management daemon and library"
arch=(x86_64)
url="https://github.com/kennylevinsen/seatd"
license=(MIT)
source=("git+$url" "sysuser.conf")
sha256sums=('SKIP')
conflicts=(seatd libseat)
provides=(seatd libseat libseat.so=1-64)
depends=(systemd)
makedepends=(meson ninja scdoc)

pkgver() {
	cd seatd
	#_ver="$(cat CMakeLists.txt | grep -m3 -e _VERSION_MAJOR -e _VERSION_MINOR -e _VERSION_PATCH | grep -o "[[:digit:]]*" | paste -sd'.')"
        #echo "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
	git describe --tags --long | sed 's/^v//;s/-/./g'
}

build() {
  mkdir -p build
  arch-meson seatd build -Dlibseat-logind=systemd \
      -D b_ndebug=true \
      -D b_pie=false \
      -D man-pages=disabled \
      -D examples=disabled \
      -D werror=false \
      -Dserver="enabled" \
      --buildtype=release \
      --wrap-mode=nofallback
  ninja -C build
}

package() {
    DESTDIR="$pkgdir/" ninja -C build install
    install -Dm644 "seatd/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"

    # Allow users in the "seat" group to access seatd, and provide a service
    # that will be necessary for e.g. wlroots-based seat management
    install -Dm644 "seatd/contrib/systemd/seatd.service" \
            -t "$pkgdir/usr/lib/systemd/system/"
    install -Dm644 "sysuser.conf" "$pkgdir/usr/lib/sysusers.d/seatd.conf"
}

