pkgname=alsa-lib
pkgver=1.2.13
pkgrel=3.1
pkgdesc="An alternative implementation of Linux sound support"
arch=(x86_64)
url="https://www.alsa-project.org"
license=(LGPL2.1)
depends=(
  alsa-topology-conf
  alsa-ucm-conf
  glibc
)
provides=(
  libasound.so
  libatopology.so
)
install=$pkgname.install
source=("git+https://github.com/alsa-project/alsa-lib.git"
        )
sha512sums=('SKIP')
b2sums=('SKIP')
validpgpkeys=('F04DF50737AC1A884C4B3D718380596DA6E59C91')

prepare() {
  cd $pkgname
  autoreconf -fiv
}

build() {
  # -flto=auto is not supported: https://github.com/alsa-project/alsa-lib/issues/110
  #CFLAGS+=" -flto-partition=none"
  cd $pkgname
  ./configure --prefix=/usr --without-debug
  # prevent excessive overlinking due to libtool
  #sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

check() {
  export LD_LIBRARY_PATH="$pkgname/src/.libs/:$LD_LIBRARY_PATH"
  make -k check -C $pkgname
}

package() {
  make DESTDIR="$pkgdir" install -C $pkgname
  install -vDm 644 $pkgname/{MEMORY-LEAK,TODO,NOTES,ChangeLog,doc/asoundrc.txt} -t "$pkgdir/usr/share/doc/$pkgname/"
}
