# Maintainer: Yurii Kolesnykov
# Based on extra/xorgproto by AndyRTR <andyrtr@archlinux.org>

_pkgname=xorgproto
pkgname=$_pkgname-git
pkgver=2023.2.18.r2764.g62323c7
pkgrel=1
pkgdesc='combined X.Org X11 Protocol headers (git version)'
arch=('any')
url="https://xorg.freedesktop.org/"
license=('custom')
makedepends=(
	'git'
	'meson'
	'xorg-util-macros'
)
checkdepends=('python-libevdev')
provides=('xorgproto')
conflicts=('xorgproto')
source=("$pkgname::git+https://gitlab.freedesktop.org/xorg/proto/xorgproto.git")
sha512sums=('SKIP')

pkgver() {
  cd "$pkgname"
  echo $(git describe --long | cut -d "-" -f2-3 | tr - .).r$(git rev-list HEAD --count).$(git describe --long | cut -d "-" -f4)
}

prepare() {
  mkdir -p build

    local src
    for src in "${source[@]}"; do
        src="${src%%::*}"
        src="${src##*/}"
        [[ $src = *.patch ]] || continue
        echo "Applying patch $src..."
        patch --directory="xorgproto-git" --forward --strip=1 < "$src"
    done
}

build() {
  arch-meson "$pkgname" build \
      -D b_ndebug=true \
      -D b_pie=false \
      -D c_std=gnu2x \
      -D cpp_std=gnu++2a \
      --buildtype=release \
      --wrap-mode=nofallback
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install

  # missing docs
  install -m755 -d "${pkgdir}/usr/share/doc/${_pkgname}"
  install -m644 "$pkgname"/PM_spec "${pkgdir}/usr/share/doc/${_pkgname}/"

  # licenses
  install -m755 -d "${pkgdir}/usr/share/licenses/${_pkgname}"
  install -m644 "$pkgname"/COPYING* "${pkgdir}/usr/share/licenses/${_pkgname}/"
  # remove licences of legacy stuff we don't ship anymore
  rm -f "${pkgdir}"/usr/share/licenses/${_pkgname}/COPYING-{evieproto,fontcacheproto,lg3dproto,printproto,xcalibrateproto,xf86rushproto}

  # cleanup
  rm -f "${pkgdir}"/usr/include/X11/extensions/apple*
  rm -f "${pkgdir}"/usr/share/licenses/${_pkgname}/COPYING-{apple,windows}wmproto
  rm -f "${pkgdir}"/usr/share/pkgconfig/applewmproto.pc
}
