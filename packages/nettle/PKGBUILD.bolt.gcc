pkgname=nettle
pkgver=3.10.1
pkgrel=4.1
pkgdesc="A low-level cryptographic library (PGO+BOLT-optimized)"
arch=('x86_64')
url="https://www.lysator.liu.se/~nisse/nettle"
license=('GPL2')
depends=('glibc' 'gmp')
makedepends=('clang' 'llvm')
provides=('nettle' 'libhogweed.so' 'libnettle.so')
conflicts=('nettle')
checkdepends=('valgrind')
options=(!strip)
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('ccfeff981b0ca71bbd6fbcb054f407c60ffb644389a5be80d6716d5b550c6ce3'
            'SKIP')
validpgpkeys=('343C2FF0FBEE5EC2EDBEF399F3599FF828C67298') # Niels MÃ¶ller

prepare() {
  cd $pkgname-$pkgver
  autoreconf -vfi
}

build() {
  cd $pkgname-$pkgver

  # Use system clang/llvm
  export CC="gcc"
  export CXX="g++"
  export LD="ld"

  # Save original flags
  export CFLAGS_SAVE="$CFLAGS"
  export CXXFLAGS_SAVE="$CXXFLAGS"
  export LDFLAGS_SAVE="$LDFLAGS"

  # Set profile directory
  local PROFILE_DIR="${srcdir}/profiles"
  mkdir -p "$PROFILE_DIR"
  local BOLT_DATA_DIR="${PROFILE_DIR}/bolt_data"
  mkdir -p "$BOLT_DATA_DIR"

  # Common configure options
  _configure_opts=(
    --prefix="/usr"
    --disable-documentation
    --enable-x86-aesni
    --enable-x86-sha-ni
    --enable-x86-pclmul
  )

  echo "Building PGO instrumented build..."

  # --- PGO Instrumentation Build ---

  CFLAGS="$CFLAGS_SAVE -fprofile-generate=$PROFILE_DIR -g3 -fno-omit-frame-pointer"
  CXXFLAGS="$CXXFLAGS_SAVE -fprofile-generate=$PROFILE_DIR -g3 -fno-omit-frame-pointer"
  LDFLAGS="$LDFLAGS_SAVE -fprofile-generate=$PROFILE_DIR -g3 -fno-omit-frame-pointer"
  export CFLAGS CXXFLAGS LDFLAGS

  ./configure "${_configure_opts[@]}"
  make -j$(nproc)

  echo "Running PGO training..."

  # --- PGO Training Run ---
  # Run the test suite multiple times to get better coverage
  for i in {1..3}; do
    echo "Training run $i..."
    if ! make -k check; then
      echo "Note: Some tests failed during PGO training (run $i)"
    fi
  done

  echo "Building PGO optimized build..."

  # --- PGO Optimization Build ---
  unset LLVM_PROFILE_FILE
  CFLAGS="$CFLAGS_SAVE -fprofile-use=${PROFILE_DIR}"
  CXXFLAGS="$CXXFLAGS_SAVE -fprofile-use=${PROFILE_DIR}"
  LDFLAGS="$LDFLAGS_SAVE -fprofile-use=${PROFILE_DIR} -Wl,--emit-relocs"
  export CFLAGS CXXFLAGS LDFLAGS

  make distclean
  ./configure "${_configure_opts[@]}"
  make -j$(nproc)

  echo "Starting BOLT Instrumentation..."

  # --- BOLT Instrumentation Phase ---
  NETTLE_LIB=$(find . -name "libnettle.so*" -type f -executable | head -n1)
  HOGWEED_LIB=$(find . -name "libhogweed.so*" -type f -executable | head -n1)

  if [[ -z "$NETTLE_LIB" || -z "$HOGWEED_LIB" ]]; then
    echo "Error: Could not find library files for BOLT instrumentation"
    exit 1
  fi

  echo "Instrumenting libraries for BOLT:"
  echo "Nettle: $NETTLE_LIB"
  echo "Hogweed: $HOGWEED_LIB"

  # Instrument nettle
  llvm-bolt \
    --instrument \
    --lite=false \
    --instrumentation-file-append-pid \
    --instrumentation-file="${BOLT_DATA_DIR}/nettle.fdata" \
    "$NETTLE_LIB" \
    -o "${NETTLE_LIB}.inst"
  if [[ $? -ne 0 || ! -f "${NETTLE_LIB}.inst" ]]; then
    echo "Error: BOLT instrumentation failed for nettle"
    exit 1
  fi
  mv "$NETTLE_LIB" "${NETTLE_LIB}.org"
  mv "${NETTLE_LIB}.inst" "$NETTLE_LIB"

  # Instrument hogweed
  llvm-bolt \
    --instrument \
    --lite=false \
    --instrumentation-file-append-pid \
    --instrumentation-file="${BOLT_DATA_DIR}/hogweed.fdata" \
    "$HOGWEED_LIB" \
    -o "${HOGWEED_LIB}.inst"
  if [[ $? -ne 0 || ! -f "${HOGWEED_LIB}.inst" ]]; then
    echo "Error: BOLT instrumentation failed for hogweed"
    exit 1
  fi
  mv "$HOGWEED_LIB" "${HOGWEED_LIB}.org"
  mv "${HOGWEED_LIB}.inst" "$HOGWEED_LIB"


  echo "Running BOLT Instrumented Training Run..."

  # --- BOLT Instrumented Training Run ---
  make -k check || true


  echo "Starting BOLT Optimization..."

  # --- BOLT Optimization Phase ---
  echo "Optimizing libraries with BOLT:"
  echo "Nettle: $NETTLE_LIB"
  echo "Hogweed: $HOGWEED_LIB"


  # Common BOLT options
  _bolt_opts=(
    --update-debug-sections
    --dyno-stats
    --lite=false
    --cu-processing-batch-size=64
    --eliminate-unreachable
    --frame-opt=all
    --icf=all
    --jump-tables=aggressive
    --min-branch-clusters
    --stoke
    --sctc-mode=always
    --plt=all
    --hot-data
    --hot-text
    --frame-opt-rm-stores
    --peepholes=all
    --infer-stale-profile=1
    --x86-strip-redundant-address-size
    --indirect-call-promotion=all
    --reg-reassign
    --use-aggr-reg-reassign
    --reorder-blocks=ext-tsp
    --reorder-functions=cdsort
    --split-all-cold
    --split-eh
    --split-functions
    --split-strategy=cdsplit
    --redirect-never-taken-jumps
  )

  # Optimize nettle
  llvm-bolt "${NETTLE_LIB}.org" \
    --data "${BOLT_DATA_DIR}/nettle.fdata" \
    "${_bolt_opts[@]}" \
    -o "${NETTLE_LIB}.bolt"
  if [[ $? -ne 0 || ! -f "${NETTLE_LIB}.bolt" ]]; then
    echo "Error: BOLT optimization failed for nettle"
    exit 1
  fi
  mv "${NETTLE_LIB}.bolt" "$NETTLE_LIB"


  # Optimize hogweed
  llvm-bolt "${HOGWEED_LIB}.org" \
    --data "${BOLT_DATA_DIR}/hogweed.fdata" \
    "${_bolt_opts[@]}" \
    -o "${HOGWEED_LIB}.bolt"
  if [[ $? -ne 0 || ! -f "${HOGWEED_LIB}.bolt" ]]; then
    echo "Error: BOLT optimization failed for hogweed"
    exit 1
  fi
  mv "${HOGWEED_LIB}.bolt" "$HOGWEED_LIB"

}

check() {
  cd $pkgname-$pkgver
  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install

  # Use llvm-strip with error handling
  find "$pkgdir" -type f \( -name '*.so*' -o -name '*.a' -o -executable \) -print0 | while IFS= read -r -d '' file; do
    if file "$file" | grep -q "ELF"; then
      if ! llvm-strip --strip-unneeded "$file" 2>/dev/null; then
        echo "Warning: Failed to strip $file"
      fi
    fi
  done
}
