# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

# Maintainer: Andreas Radke <andyrtr@archlinux.org>
# Contributor: bender02 at gmx dot com

pkgname=nettle
pkgver=3.9.1
pkgrel=2.1
pkgdesc="A low-level cryptographic library"
arch=('x86_64')
url="https://www.lysator.liu.se/~nisse/nettle"
license=('GPL2')
depends=('glibc' 'gmp')
provides=('nettle' 'libhogweed.so' 'libnettle.so')
conflicts=('nettle')
checkdepends=('valgrind')
source=(https://ftp.gnu.org/gnu/$pkgname/$pkgname-$pkgver.tar.gz{,.sig})
sha256sums=('ccfeff981b0ca71bbd6fbcb054f407c60ffb644389a5be80d6716d5b550c6ce3'
            'SKIP')
validpgpkeys=('343C2FF0FBEE5EC2EDBEF399F3599FF828C67298') # Niels MÃ¶ller <nisse@lysator.liu.se>

build() {
  cd $pkgname-$pkgver

  autoreconf -vfi
  ./configure \
    --prefix="/usr" \
    --disable-documentation \
    --enable-x86-aesni \
    --enable-x86-sha-ni \
    --enable-x86-pclmul

  make
}

check() {
  cd $pkgname-$pkgver

  make -k check
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir/" install
}
