# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: DrZaius <lou[at]fakeoutdoorsman[dot]com>
# Contributor: pumbur

pkgbase=opus
pkgname=(
  opus
)
pkgver=1.6.1
pkgrel=3.1
pkgdesc="Totally open, royalty-free, highly versatile audio codec"
url="https://www.opus-codec.org/"
arch=(x86_64)
license=(BSD-3-Clause)
depends=(
  glibc
)
makedepends=(
  doxygen
  meson
)
source=("https://downloads.xiph.org/releases/opus/opus-$pkgver.tar.gz")
b2sums=('5ba1d6f28594f366b545507bafb22751e15a0e78e152e7cdef456dccb0bc9fc512faa18c90fb4ea5455a9535de89df987dea8a0fabce9a25c285d0c410d4b482')

# https://downloads.xiph.org/releases/opus/SHA256SUMS.txt
sha256sums=('b84610959b8d417b611aa12a22565e0a3732097c6389d19098d844543e340f85')

prepare() {
  ln -s opus-$pkgver opus
  cd opus
}

build() {
  local meson_options=(
    -D asm=disabled
    -D docs=disabled
    -D custom-modes=true
    -D deep-plc=enabled
    -D dred=enabled
    -D osce=enabled
  )

  arch-meson opus build "${meson_options[@]}" \
    -D b_ndebug=true \
    -D b_pie=false \
    -D c_std=gnu18 \
    -D cpp_std=gnu++2a \
      --buildtype=release \
      --wrap-mode=nofallback
  meson compile -C build
}

#check() {
#  meson test -C build --print-errorlogs
#}

package_opus() {
  provides=(libopus.so)

  meson install -C build --destdir "$pkgdir"
  install -Dt "$pkgdir/usr/share/aclocal" -m644 opus/opus.m4

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 opus/COPYING
}

# vim:set sw=2 sts=-1 et:
