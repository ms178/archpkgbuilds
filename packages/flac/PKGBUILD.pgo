pkgbase=flac
pkgname=('flac')
pkgver=1.5.0
pkgrel=3.2
pkgdesc='Free Lossless Audio Codec (PGO and BOLT optimized)'
url='https://xiph.org/flac/'
arch=('x86_64')
license=('BSD' 'GPL')
depends=('gcc-libs' 'libogg')
makedepends=('nasm' 'doxygen' 'cmake' 'ninja' 'llvm')
source=(https://downloads.xiph.org/releases/flac/flac-${pkgver}.tar.xz)
sha512sums=('4a626e8a1bd126e234c0e5061e3b46f3a27c2065fdfa228fd8cf00d3c7fa2c05fafb5cec36acce7bfce4914bfd7db0b2a27ee15decf2d8c4caad630f62d44ec9')

prepare() {
  cd flac-${pkgver}
}

build() {
  # Save original flags
  CFLAGS_ORIG="$CFLAGS"
  CXXFLAGS_ORIG="$CXXFLAGS"
  LDFLAGS_ORIG="$LDFLAGS"

  # Step 1: Build instrumented binaries for PGO
  export LLVM_PROFILE_FILE="$srcdir/build-pgo-instrument/%p.profraw"
  CFLAGS+=" -fprofile-generate -fno-omit-frame-pointer -mllvm -vp-counters-per-site=10 -fdebug-info-for-profiling"
  CXXFLAGS+=" -fno-omit-frame-pointer -fprofile-generate -mllvm -vp-counters-per-site=10 -fdebug-info-for-profiling"
  LDFLAGS+=" -fno-omit-frame-pointer -fprofile-generate -mllvm -vp-counters-per-site=10 -fdebug-info-for-profiling"

  cmake -S flac-${pkgver} -B build-pgo-instrument -G Ninja \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_MANDIR=/usr/share/man/man1 \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_DOCS=OFF \
    -DBUILD_EXAMPLES=OFF \
    -DINSTALL_MANPAGES=OFF \
    -DWITH_STACK_PROTECTOR=OFF \
    -DNDEBUG=OFF
  cmake --build build-pgo-instrument

  # Step 2: Run tests to generate PGO profile data
  cd build-pgo-instrument
  ctest --output-on-failure --stop-on-failure -j1
  cd ..

  # Step 3: Merge raw profile data
  llvm-profdata merge -output="$srcdir/build-pgo-instrument/profile.profdata" "$srcdir/build-pgo-instrument"/*.profraw

  # Step 4: Build with PGO optimizations while preserving debug info for BOLT
  # Reset flags to original values
  CFLAGS="$CFLAGS_ORIG"
  CXXFLAGS="$CXXFLAGS_ORIG"
  LDFLAGS="$LDFLAGS_ORIG"

  # Add flags needed for both PGO and BOLT
  CFLAGS+=" -fprofile-use=$srcdir/build-pgo-instrument/profile.profdata"
  CXXFLAGS+=" -fprofile-use=$srcdir/build-pgo-instrument/profile.profdata"
  LDFLAGS+=" -fprofile-use=$srcdir/build-pgo-instrument/profile.profdata"

  cmake -S flac-${pkgver} -B build-pgo-optimized -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23 \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_MANDIR=/usr/share/man/man1 \
    -DBUILD_SHARED_LIBS=ON \
    -DBUILD_DOCS=OFF \
    -DBUILD_EXAMPLES=OFF \
    -DINSTALL_MANPAGES=OFF \
    -DWITH_STACK_PROTECTOR=OFF \
    -DNDEBUG=ON
  cmake --build build-pgo-optimized
}

package_flac() {
  provides=('libFLAC.so' 'libFLAC.so=8-64' 'libFLAC++.so')

  DESTDIR="${pkgdir}" cmake --install build-pgo-optimized

  install -Dm 644 flac-${pkgver}/src/*/*.m4 -t "${pkgdir}/usr/share/aclocal"

  install -Dm 644 flac-${pkgver}/COPYING.Xiph -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
