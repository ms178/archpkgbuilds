# Maintainer:  Vincent Grande <shoober420@gmail.com>
# Contributor: Christer Solskogen <christer.solskogen@gmail.com>
# Contributor: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jameson Pugh <imntreal@gmail.com>
# Contributor: J0k3r <moebius282@gmail.com>

pkgname=lib32-sdl3-git
pkgver=prerelease.3.0.0.r833.gc708ddd66
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Version 2. 32 -bit)"
arch=('x86_64')
url="https://www.libsdl.org"
license=('MIT')
depends=(
  lib32-glibc
  lib32-libxext
  lib32-libxrender
  lib32-libx11
  lib32-libgl
  lib32-libxcursor
  lib32-libxxf86vm
  sdl2
)
makedepends=(
  cmake
  jack
  lib32-alsa-lib
  lib32-mesa
  lib32-libpulse
  lib32-libxrandr
  lib32-libxinerama
  lib32-wayland
  lib32-libxkbcommon
  lib32-libxss
  lib32-libxxf86vm
  git
  wayland-protocols
)
optdepends=(
  'lib32-alsa-lib: ALSA audio driver'
  'lib32-libpulse: PulseAudio audio driver'
  'lib32-jack: JACK audio driver'
  'lib32-pipewire: Pipewire audio driver'
)
provides=(lib32-sdl3=3.0.0)
conflicts=(lib32-sdl3 lib32-sdl3-minimal-hg)
source=("git+https://github.com/libsdl-org/SDL")
sha512sums=('SKIP')

pkgver() {
  cd SDL
  git describe --long --tags | sed 's/^release-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  #fix libdir
  sed -i 's|lib/cmake|lib32/cmake|' SDL/CMakeLists.txt

  # Don't try to link against ibus
  sed -i '/pkg_search_module.*ibus-1.0/d' SDL/CMakeLists.txt

  rm -rf build
  mkdir build
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cmake -S SDL -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DLIB_SUFFIX=32 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
	-D SDL_RPI=OFF \
	-D SDL_STATIC=OFF \
	-D SDL_RPATH=OFF
  make -C build
}

package() {
  
  DESTDIR="${pkgdir}" cmake --install build
  rm -rf "${pkgdir}"/usr/{bin,include,share}
  #sed -i "s/libSDL2\.a/libSDL2main.a/g" "$pkgdir"/usr/lib32/cmake/SDL2/SDL2Targets-noconfig.cmake
  install -dm 755 "${pkgdir}"/usr/share/licenses
  ln -s sdl3 "${pkgdir}"/usr/share/licenses/lib32-sdl3
}

# vim: ts=2 sw=2 et:
