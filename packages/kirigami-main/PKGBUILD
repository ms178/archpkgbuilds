# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kirigami
pkgver=5.112.0
pkgrel=3.1
pkgdesc='A QtQuick based components set'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(gcc-libs
         glibc
         qt5-base
         qt5-declarative
         qt5-svg)
makedepends=(doxygen
             extra-cmake-modules
             qt5-doc
             #qt5-shadertools
             qt5-tools)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('7f3d175347bf50e904821d910e160a12d2d4e577c84b8aaf193fdc284c3c83fc'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB  # David Faure <faure@kde.org>
              E0A3EB202F8E57528E13E72FD7574483BB57B18D) # Jonathan Esk-Riddell <jr@jriddell.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release  \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}

