pkgname=mold-git
pkgver=v2.36.0_2_gbe087071
pkgrel=1
pkgdesc="A Modern Linker"
arch=(x86_64)
url="https://github.com/rui314/mold"
license=("AGPL3")
# xxhash is bundled
depends=('gcc-libs' 'mimalloc' 'openssl' 'zlib' 'onetbb')
makedepends=('git' 'python' 'cmake' 'mold' 'llvm')
checkdepends=('clang' 'libdwarf')
source=("mold::git+https://github.com/rui314/mold")
sha256sums=('SKIP')
provides=("mold=$pkgver")
options=(!lto !strip)
conflicts=("mold")
reponame="mold"

pkgver() {
    cd "$reponame"
    git describe --long --tags | sed "s/-/_/g"
}

build() {
  # First build - Generate profile data
  cmake \
    -S "$reponame" \
    -B build-pgo \
    -DCMAKE_BUILD_TYPE='Debug' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBEXECDIR='lib' \
    -DMOLD_USE_SYSTEM_MIMALLOC=OFF \
    -DMOLD_USE_SYSTEM_TBB=OFF \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23 \
    -DMOLD_USE_MOLD=OFF \
    -DMOLD_LTO=OFF \
    -DCMAKE_C_FLAGS="-fprofile-generate -fno-omit-frame-pointer -g3 -mllvm -vp-counters-per-site=6" \
    -DCMAKE_CXX_FLAGS="-fprofile-generate -fno-omit-frame-pointer -g3 -mllvm -vp-counters-per-site=6"

  cmake --build build-pgo

  # Run tests to generate profile data
  cd build-pgo
  ctest --output-on-failure
  cd ..

  # Merge profile data
  llvm-profdata merge -output=build-pgo/default.profdata build-pgo/default*.profraw

  # Second build - Use profile data with debug info for BOLT
  cmake \
    -S "$reponame" \
    -B build \
    -DCMAKE_BUILD_TYPE='Release' \
    -DCMAKE_INSTALL_PREFIX='/usr' \
    -DCMAKE_INSTALL_LIBEXECDIR='lib' \
    -DMOLD_USE_SYSTEM_MIMALLOC=OFF \
    -DMOLD_USE_SYSTEM_TBB=OFF \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23 \
    -DMOLD_USE_MOLD=OFF \
    -DMOLD_LTO=ON \
    -DCMAKE_C_FLAGS="-fprofile-use=/tmp/makepkg/mold-git/src/build-pgo/default.profdata" \
    -DCMAKE_CXX_FLAGS="-fprofile-use=/tmp/makepkg/mold-git/src/build-pgo/default.profdata" \
    -DCMAKE_EXE_LINKER_FLAGS="-Wl,--emit-relocs" \
    -DCMAKE_MODULE_LINKER_FLAGS="-Wl,--emit-relocs" \
    -DCMAKE_SHARED_LINKER_FLAGS="-Wl,--emit-relocs"

  cmake --build build

  # Create perf data for BOLT
  cd build
  perf record -e cycles:u -j any,u -o perf.data -- ctest --output-on-failure
  cd ..

  # Process with BOLT
  llvm-bolt build/mold \
    --data=build/perf.data \
    --dyno-stats \
    --cu-processing-batch-size=64 \
    --eliminate-unreachable\
    --frame-opt=all \
    --icf=all \
    --jump-tables=aggressive \
    --min-branch-clusters \
    --stoke \
    --sctc-mode=always \
    --plt=all \
    --hot-data \
    --hot-text \
    --frame-opt-rm-stores \
    --peepholes=all \
    --infer-stale-profile=1 \
    --x86-strip-redundant-address-size \
    --indirect-call-promotion=all \
    --reg-reassign \
    --use-aggr-reg-reassign \
    --reorder-blocks=ext-tsp \
    --reorder-functions=cdsort \
    --split-all-cold \
    --split-eh \
    --split-functions \
    --split-strategy=cdsplit \
    -o build/mold.bolt

  # Replace original binary with BOLTed one
  mv build/mold.bolt build/mold
}

check() {
  ctest --test-dir build --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
