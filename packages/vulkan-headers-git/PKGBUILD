# Maintainer: Lu√≠s Ferreira <contact@lsferreira.net>

pkgname=vulkan-headers-git
pkgver=1.3.213.r0.g33d4dd9
pkgrel=1
pkgdesc="Vulkan header files"
groups=('vulkan-devel')
arch=('any')
url="https://github.com/KhronosGroup/Vulkan-Headers"
provides=('vulkan-headers' 'vulkan-hpp')
license=('Apache')
conflicts=('vulkan-headers')
source=("git+https://github.com/KhronosGroup/Vulkan-Headers.git")
sha512sums=('SKIP')
makedepends=(cmake git)

pkgver() {
  cd "Vulkan-Headers"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}


build() {
  cd "$srcdir/Vulkan-Headers"

  rm -rf build ; mkdir build ; cd build
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF \
    ..
  make
}

package() {
  cd "$srcdir/Vulkan-Headers/build"

  make DESTDIR="${pkgdir}" install
}
