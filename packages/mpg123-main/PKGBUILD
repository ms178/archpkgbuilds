# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=mpg123
pkgver=1.32.2
pkgrel=3.1
pkgdesc='Console based real time MPEG Audio Player for Layer 1, 2 and 3'
url="https://sourceforge.net/projects/mpg123"
arch=('x86_64')
license=('LGPL2.1')
depends=('alsa-lib')
makedepends=('sdl2' 'jack' 'libpulse')
optdepends=('sdl2: for sdl audio support'
            'pipewire-jack: for jack audio support'
            'libpulse: for pulse audio support'
            'perl: for conplay')
provides=(libmpg123.so libout123.so)
source=(https://downloads.sourceforge.net/sourceforge/${pkgname}/${pkgname}-${pkgver}.tar.bz2{,.sig})
sha512sums=('084f4575d3ad88373a04035778b40e4871b6da969f42b426c76d9539632baa12534d7f0f9b976be228fd313dea9c31f7a259e0a8b56d044c7e89fefdf897def2'
            'SKIP')
validpgpkeys=('D021FF8ECF4BE09719D61A27231C4CBC60D5CAFE')

build() {
  cd ${pkgname}-${pkgver}
  autoreconf -vfi
  ./configure \
    --prefix=/usr \
    --enable-int-quality \
    --with-audio="alsa oss sdl jack pulse"
  # https://bugzilla.gnome.org/show_bug.cgi?id=655517
  #sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 755 scripts/conplay "${pkgdir}/usr/bin/conplay"
}

# vim: ts=2 sw=2 et:
