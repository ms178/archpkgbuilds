# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>

pkgname=libnghttp3
pkgver=1.6.0
pkgrel=2.1
pkgdesc="HTTP/3 library written in C"
url='https://github.com/ngtcp2/nghttp3'
arch=('x86_64')
license=('MIT')
provides=('libnghttp3.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/ngtcp2/nghttp3/releases/download/v${pkgver}/nghttp3-${pkgver}.tar.gz")
sha256sums=('SKIP')

prepare() {
  cd nghttp3-${pkgver}
  autoreconf -vfi
}

build() {
  cd nghttp3-${pkgver}
  ./configure \
    --prefix=/usr
  make
}

check() {
  cd nghttp3-${pkgver}
  make check
}

package() {
  cd nghttp3-${pkgver}/lib
  make DESTDIR="${pkgdir}" install
  install -Dm644 ../COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
