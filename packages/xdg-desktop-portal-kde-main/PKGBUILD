# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=xdg-desktop-portal-kde
pkgver=5.27.8
_dirver=$(echo $pkgver | cut -d. -f1-3)
pkgrel=5.1
pkgdesc='A backend implementation for xdg-desktop-portal using Qt/KF5'
arch=(x86_64)
url='https://kde.org/plasma-desktop/'
license=(LGPL)
depends=(kirigami2 plasma-framework kio-fuse xdg-desktop-portal)
makedepends=(extra-cmake-modules plasma-wayland-protocols wayland-protocols)
provides=(xdg-desktop-portal-impl)
groups=(plasma)
source=(https://download.kde.org/stable/plasma/$_dirver/$pkgname-$pkgver.tar.xz{,.sig}
        https://invent.kde.org/plasma/xdg-desktop-portal-kde/-/commit/33f8d622.patch)
sha256sums=('7e1fdf244c6363a73f487c5e9f854b08885cda74aa7f429bd9ceb4c35c273f53'
            'SKIP'
            '8ff96b11c9c625a768ca86673b6a2d188b49f0de9e47c07de771ebc2aacf8f5b')
validpgpkeys=('E0A3EB202F8E57528E13E72FD7574483BB57B18D'  # Jonathan Esk-Riddell <jr@jriddell.org>
              '0AAC775BB6437A8D9AF7A3ACFE0784117FBCE11D'  # Bhushan Shah <bshah@kde.org>
              'D07BD8662C56CB291B316EB2F5675605C74E02CF'  # David Edmundson <davidedmundson@kde.org>
              '1FA881591C26B276D7A5518EEAAF29B42A678C20') # Marco Martin <notmart@gmail.com>

prepare() {
  patch -d $pkgname-$pkgver -p1 < 33f8d622.patch # Fallback to GTK por settings
}

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_BUILD_TYPE=Release  \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
