# Maintainer: George Rawlinson <grawlinson@archlinux.org>
# Contributor: Ren√© Wagner < rwagner at rw-net dot de >
# Contributor: Diab Neiroukh <lazerl0rd@thezest.dev>

pkgname=lib32-mimalloc
pkgver=2.2.5
pkgrel=3.1
pkgdesc='General-purpose allocator with excellent performance characteristics'
arch=('x86_64')
url='https://github.com/microsoft/mimalloc'
license=('MIT')
depends=('glibc')
makedepends=('git' 'cmake')
source=("$pkgname::git+$url#tag=v${pkgver}")
b2sums=('SKIP')

pkgver() {
  cd "$pkgname"

  git describe --tags | sed 's/^v//'
}

build() {

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export ASMFLAGS="-m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  export LDFLAGS=' -m32'

  cmake \
    -B build \
    -S "$pkgname" \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DCMAKE_BUILD_TYPE=Release  \
    -DCMAKE_UNITY_BUILD=ON \
    -D MI_BUILD_STATIC=ON \
    -D MI_BUILD_OBJECT=OFF \
    -D MI_INSTALL_TOPLEVEL=ON

  cmake --build build
}

check() {
  cd build

  ctest --output-on-failure
}

package() {

  # Remove conflicting header files
  rm -rf "$pkgdir/usr/include"

  DESTDIR="$pkgdir" cmake --install build

  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE"
}
