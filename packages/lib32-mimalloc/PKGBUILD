pkgname=lib32-mimalloc
_pkgname=mimalloc
pkgver=2.2.7
pkgrel=2.1
pkgdesc='General-purpose allocator with excellent performance characteristics (32-bit)'
arch=('x86_64')
url="https://github.com/microsoft/mimalloc"
license=('MIT')
depends=('glibc' 'mimalloc')
makedepends=('git' 'cmake')
source=("$pkgname::git+$url#tag=v${pkgver}" ms178-1.patch)
b2sums=('SKIP')

prepare() {
  cd "${srcdir}/lib32-mimalloc"

  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    patch -Np1 -i "../${src}"
  done
}

pkgver() {
  cd "$pkgname"
  git describe --tags | sed 's/^v//'
}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cmake -S "$pkgname" -B build \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DCMAKE_BUILD_TYPE=Release \
    -DMI_BUILD_SHARED=ON \
    -DMI_BUILD_STATIC=OFF \
    -DMI_BUILD_OBJECT=OFF \
    -DMI_INSTALL_TOPLEVEL=OFF # Correctly set to OFF

  cmake --build build
}

check() {
  cd build
  ctest --output-on-failure
}

package() {
  DESTDIR="$pkgdir" cmake --install build
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" "$pkgname/LICENSE"
}
