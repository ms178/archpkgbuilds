# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=lib32-cairo
pkgname=(
  lib32-cairo
)
pkgver=1.18.4+12+g7fd0cd0a9
pkgrel=1
pkgdesc="2D graphics library with support for multiple output devices (32-bit)"
url="https://cairographics.org/"
arch=(x86_64)
license=('LGPL-2.1-only OR MPL-1.1')
depends=(
  cairo
  lib32-fontconfig
  lib32-freetype2
  lib32-glib2
  lib32-glibc
  lib32-libpng
  lib32-libx11
  lib32-libxcb
  lib32-libxext
  lib32-libxrender
  lib32-pixman
  lib32-zlib
)
makedepends=(
  git
  meson
  valgrind
)
source=("git+https://gitlab.freedesktop.org/cairo/cairo.git")
b2sums=('ff661ec100a24a457c91b928ad537e783e59ffc56812aa1d1da09bfe425390f8e6df0191a7993aeb56e9b064403a389d252e64f557f09ea6f773f821a021c9ca')

pkgver() {
  cd cairo
  git describe --tags | sed 's/-/+/g'
}

prepare() {

    export CC='clang -m32'
    export CXX='clang++ -m32'
    export LD='lld -m32'
    export ASMFLAGS="-m32"
    export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'

  cd cairo
}

build() {
  local meson_options=(
    -D dwrite=disabled
    -D gtk_doc=false
    -D spectre=disabled
    -D symbol-lookup=disabled
    -D tests=disabled
  )

  arch-meson cairo build "${meson_options[@]}" \
    --prefix /usr \
    --libdir lib32 \
    -D b_ndebug=true \
    -D b_pie=false \
    -D c_std=gnu18 \
    -D cpp_std=gnu++2a \
    --buildtype=release
  meson compile -C build
}

package_lib32-cairo() {
  provides=(
    libcairo-gobject.so
    libcairo-script-interpreter.so
    libcairo.so
  )

  meson install -C build --destdir "$pkgdir"

  rm -r "$pkgdir"/usr/{include,bin}
}

# vim:set sw=2 sts=-1 et:
