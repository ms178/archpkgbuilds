# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=lib32-libffi-git
pkgver=3.4.4.r27.gac598b7f
pkgrel=1
pkgdesc="Portable foreign function interface library"
arch=('i686' 'x86_64')
url="https://sourceware.org/libffi/"
license=('MIT')
depends=(lib32-glibc lib32-libffi)
checkdepends=(dejagnu)
provides=(libffi.so=8-32 'lib32-libffi')
conflicts=('lib32-libffi')
options=('staticlibs')
source=("git+https://github.com/libffi/libffi.git")
sha256sums=('SKIP')


pkgver() {
  cd "libffi"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "libffi"

  export CC="clang -m32"
  export ASMFLAGS="-m32"
  autoreconf -vfi
  ./configure \
    --build=i686-pc-linux-gnu \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --disable-static \
    --disable-docs \
    --disable-debug \
    --disable-multi-os-directory \
    --disable-exec-static-tramp \
    --enable-pax_emutramp
  make
}

check() {
  cd "libffi"

  #make check
}

package() {
  cd "libffi"

  make DESTDIR="${pkgdir}" install

  rm -r "${pkgdir}"/usr/{include,share}
  install -m755 -d "${pkgdir}/usr/share/licenses/${pkgname}"
  install -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/"
}
