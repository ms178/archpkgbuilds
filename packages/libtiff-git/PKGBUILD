# Maintainer:

pkgname=libtiff-git
pkgver=4.4.0.r114.g5b1f85b5
pkgrel=1
pkgdesc="TIFF library and utilities"
arch=('x86_64')
url="https://libtiff.gitlab.io/libtiff/"
license=('custom')
depends=('glibc' 'libjpeg-turbo' 'libjpeg.so' 'zlib' 'xz' 'zstd')
makedepends=('freeglut' 'glu' 'mesa' 'jbigkit')
optdepends=('freeglut: for using tiffgt')
provides=('libtiff' 'libtiff-git' 'libtiff=4.4.0' 'libtiff.so.5' 'libtiff.so' 'libtiffxx.so')
conflicts=(libtiff)
options=('strip')
source=("git+https://gitlab.com/libtiff/libtiff.git")
sha256sums=('SKIP')


pkgver() {
  cd "libtiff"

  git describe --long --tags | sed 's/^Release-v//;s/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "libtiff"

  autoreconf -vfi
  ./configure \
    --prefix="/usr" \
    --sysconfdir=/etc \
    --enable-ld-version-script \
    --disable-sphinx \
    --disable-docs \
    --disable-sphinx \
    --disable-tests \
    --enable-cxx \
    --disable-old-jpeg \
    --with-x
  make
}

package() {
  cd libtiff
  make DESTDIR="${pkgdir}" install
}
