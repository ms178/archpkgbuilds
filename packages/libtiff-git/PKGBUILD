# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=libtiff-git
pkgver=4.4.0.r96.g115e1aea
pkgrel=1
pkgdesc="TIFF library and utilities"
arch=('i686' 'x86_64')
url="https://libtiff.gitlab.io/libtiff/"
license=('custom')
depends=('glibc' 'libjpeg' 'xz' 'zlib')
makedepends=('git' 'freeglut' 'glu' 'jbigkit' 'mesa')
optdepends=('freeglut: for using tiffgt')
provides=('libtiff' 'libtiff=4.4.0')
conflicts=('libtiff')
options=('strip')
source=("git+https://gitlab.com/libtiff/libtiff.git")
sha256sums=('SKIP')


pkgver() {
  cd "libtiff"

  git describe --long --tags | sed 's/^Release-v//;s/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "libtiff"

  autoreconf -vfi
  ./configure \
    --prefix="/usr" \
    --disable-sphinx \
    --disable-docs \
    --enable-cxx \
    --with-x
  make
}

package() {
  cd "libtiff"

  make DESTDIR="$pkgdir" install
}
