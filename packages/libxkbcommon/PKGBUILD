# Maintainer: none

pkgname=libxkbcommon-git
pkgver=1.4.1.r0.g57af9cb
pkgrel=1
pkgdesc="Keymap handling library for toolkits and window systems"
url="http://xkbcommon.org/"
arch=(i686 x86_64)
license=('custom')
depends=(xkeyboard-config glibc)
makedepends=(libxcb doxygen xorg-util-macros meson git graphviz
			 wayland wayland-protocols)
provides=('libxkbcommon' 'libxkbcommon-x11')
conflicts=('libxkbcommon' 'libxkbcommon-x11')
source=(git+https://github.com/xkbcommon/libxkbcommon)
sha1sums=('SKIP')

pkgver() {
	cd libxkbcommon
	git describe --long | sed -r 's/^xkbcommon.//;s/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    # although removing _build folder in build() function feels more natural,
    # that interferes with the spirit of makepkg --noextract
    if [  -d _build ]; then
        rm -rf _build
    fi
}

build () {
    meson setup libxkbcommon _build \
       -D enable-docs=false \
       -D b_ndebug=true \
       -D c_std=gnu18 \
       -D cpp_std=gnu++2a \
       -D b_pie=false \
       --buildtype=release \
       --wrap-mode=nofallback \
       -D prefix=/usr \
       -D sysconfdir=/etc \

    meson configure _build

    ninja $NINJAFLAGS -C _build
}

package() {
    DESTDIR="${pkgdir}" ninja $NINJAFLAGS -C _build install
}
