# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=kcolorscheme
pkgver=5.113.0
pkgrel=3.1
pkgdesc='Classes to read and interact with KColorScheme'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(gcc-libs
         glibc
         kconfig
         kguiaddons
         ki18n
         qt5-base)
makedepends=(doxygen
             extra-cmake-modules
             qt5-doc
             qt5-tools)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('f987e02735daf3255213a171ece023bca1bca4923940ca9ec2171d483d48e41d'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB  # David Faure <faure@kde.org>
              E0A3EB202F8E57528E13E72FD7574483BB57B18D) # Jonathan Esk-Riddell <jr@jriddell.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release  \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
