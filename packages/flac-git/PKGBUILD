# Maintainer: Evelyn Marie <evelynelizabethmarie@gmail.com>

pkgbase=flac-git
pkgname=(flac-git)
pkgver=1.3.3.r203.g9e143766
pkgrel=1
arch=(x86_64 aarch64)
url="https://xiph.org/flac/"
license=(BSD GPL)
depends=(gcc-libs libogg)
makedepends=(git nasm doxygen)
source=("git+https://github.com/xiph/flac.git")
sha256sums=('SKIP')

pkgver() {
  cd "flac"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd "flac"
  ./autogen.sh
}

build() {
  cd "flac"
  ./configure --prefix=/usr --enable-64-bit-words --disable-documentation
  make
}

package_flac-git() {
  pkgdesc="Free Lossless Audio Codec"
  options=(!docs)
  conflicts=(flac)
  provides=(flac libFLAC.so libFLAC++.so libFLAC.so=8-64)

  cd "flac"
  make DESTDIR="${pkgdir}" install
  install -D -m644 COPYING.Xiph "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
