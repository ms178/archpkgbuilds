# Maintainer : Daniel Bermond < gmail-com: danielbermond >
# Contributor: Det <nimetonmaili g-mail>
# Contributor: Matt Parnell /ilikenwf <parwok@gmail.com>
# Contributor: Jonathan <eyeswide@gmail.com>

pkgname=libpciaccess-git
_srcname=libpciaccess
pkgver=0.17.r9.g6cd5a4a
pkgrel=1
pkgdesc='X11 PCI access library (git version)'
arch=('i686' 'x86_64')
url='https://cgit.freedesktop.org/xorg/lib/libpciaccess/'
license=('custom')
depends=('glibc')
makedepends=('git' 'xorg-util-macros')
provides=('libpciaccess')
conflicts=('libpciaccess')
source=('git+https://anongit.freedesktop.org/git/xorg/lib/libpciaccess.git')
sha256sums=('SKIP')


prepare() {
  mkdir build
}

build() {
  arch-meson "$_srcname" build \
      -D b_ndebug=true \
      -D b_pie=false \
      -D c_std=gnu2x \
      -D cpp_std=gnu++2a \
      -D b_lto=true \
      --buildtype=release \
      --wrap-mode=nofallback
  ninja -C build
}

check() {
  meson test -C build
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -Dm644 "$_srcname"/COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}/"
}
