# Maintainer:  Vincent Grande <shoober420@gmail.com>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Brice Carpentier <brice@daknet.org>

pkgbase=cairo
pkgname=(
  cairo-git
)
pkgver=1.18.0+80+g01a8ca051
pkgrel=1
pkgdesc="2D graphics library with support for multiple output devices"
url="https://cairographics.org/"
arch=(x86_64)
license=(LGPL MPL)
depends=(lzo zlib libpng fontconfig freetype2 libx11 libxext libxrender libxcb
         glib2 pixman)
makedepends=(valgrind git meson)
provides=(cairo)
conflicts=(cairo)
source=("git+https://gitlab.freedesktop.org/cairo/cairo.git")
sha256sums=('SKIP')

pkgver() {
  cd cairo
  git describe --tags | sed 's/-/+/g'
}

build() {
  arch-meson cairo build \
    -D spectre=disabled \
    -D python.bytecompile=2 \
    -D dwrite=disabled \
    -D gtk_doc=false \
    -D tee=enabled \
    -D xlib-xcb=enabled \
    -D b_ndebug=true \
    -D b_pie=false \
    -D c_std=gnu18 \
    -D cpp_std=gnu++2a \
    --buildtype=release \
    -D quartz=disabled \
    -D symbol-lookup=disabled \
    -D tests=disabled
  meson compile -C build
}

package() {
  provides=(
    libcairo-gobject.so
    libcairo-script-interpreter.so
    libcairo.so
    cairo
    cairo-git
  )

  meson install -C build --destdir "$pkgdir"

}
