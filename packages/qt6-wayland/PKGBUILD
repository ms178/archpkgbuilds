# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=qt6-wayland
pkgver=6.7.2
pkgrel=5.1
arch=(x86_64)
url='https://www.qt.io'
license=(GPL3 LGPL3 FDL custom)
pkgdesc='Provides APIs for Wayland'
depends=(gcc-libs
         glibc
         libglvnd
         libxkbcommon
         qt6-base
         qt6-declarative
         wayland)
makedepends=(cmake
             git
             ninja)
groups=(qt6)
_pkgfn=${pkgname/6-/}
source=(git+https://code.qt.io/qt/$_pkgfn#tag=v$pkgver)
sha256sums=('8720175eff788d84bb9dac241aed28bfa61f85411d29ac76776b98ddd1e700d7')

prepare() {
  cd $_pkgfn

  git cherry-pick -n 92bcb8f6b7a852c7a5d662fc34de561692a7a454
  git cherry-pick -n c2f61bc47baacf2e6a44c6c3c4e4cbf0abfa4095
  git cherry-pick -n 406995207eae8d644b6e5262aa716a48c7e471a8
  git cherry-pick -n 632127d7f1d86cba4dd17361f24f9fd70a0ae44c
}

build() {
  cmake -B build -S $_pkgfn -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_UNITY_BUILD=ON \
    -DCMAKE_MESSAGE_LOG_LEVEL=STATUS
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build

  install -d "$pkgdir"/usr/share/licenses
  ln -s /usr/share/licenses/qt6-base "$pkgdir"/usr/share/licenses/$pkgname
}
