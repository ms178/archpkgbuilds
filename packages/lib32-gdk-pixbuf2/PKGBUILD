# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgbase=lib32-gdk-pixbuf2
pkgname=(
  lib32-gdk-pixbuf2
)
pkgver=2.44.2
pkgrel=2.1
pkgdesc="An image loading library (32-bit)"
url="https://wiki.gnome.org/Projects/GdkPixbuf"
arch=(x86_64)
license=(LGPL-2.0-or-later)
depends=(
  gdk-pixbuf2
  lib32-glib2
  lib32-glibc
  lib32-libjpeg-turbo
  lib32-libpng
  lib32-libtiff
  shared-mime-info
)
makedepends=(
  git
  glib2-devel
  meson
)
source=(
  "git+https://gitlab.gnome.org/GNOME/gdk-pixbuf.git#tag=$pkgver"
  gdk-pixbuf-query-loaders-32.hook
)
b2sums=('SKIP')

prepare() {
  cd gdk-pixbuf
}

build() {
  local meson_options=(
    --libdir /usr/lib32
    -D android=disabled
    -D builtin_loaders=all
    -D documentation=false
    -D gif=enabled
    -D glycin=disabled
    -D gtk_doc=false
    -D installed_tests=false
    -D introspection=disabled
    -D jpeg=enabled
    -D man=false
    -D others=enabled
    -D png=enabled
    -D thumbnailer=disabled
    -D tiff=enabled
    -D b_ndebug=true
    -D b_pie=false
    -D c_std=gnu2x
    -D cpp_std=gnu++2a
    -D b_lto=true
    --buildtype=release
    --wrap-mode=nofallback
  )

  export CC="clang -m32"
  export CXX="clang++ -m32"
  export ASMFLAGS="-m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson gdk-pixbuf build "${meson_options[@]}"
  meson compile -C build
}

check() {
  meson test -C build --print-errorlogs
}

package() {
  optdepends=(
    "lib32-librsvg: Load .svg, .svgz and .svg.gz"
  )
  provides=(libgdk_pixbuf-2.0.so)
  install=lib32-gdk-pixbuf2.install

  meson install -C build --destdir "$pkgdir"

  rm -rf "$pkgdir"/usr/{include,lib,share}
  find "$pkgdir/usr/bin" -type f -not -name gdk-pixbuf-query-loaders -delete
  mv "$pkgdir"/usr/bin/gdk-pixbuf-query-loaders{,-32}

  install -Dt "$pkgdir/usr/share/libalpm/hooks" -m644 *.hook
}

# vim:set sw=2 sts=-1 et:
