# Maintainer: Antonio Rojas <arojas@archlinux.org>

_name=kuserfeedback
pkgname=${_name}5
pkgver=1.3.0
pkgrel=5.1
pkgdesc='Framework for collecting user feedback for applications via telemetry and surveys'
arch=(x86_64)
url='https://kde.org/products/frameworks/'
license=(GPL)
depends=(gcc-libs
         glibc
         qt5-base)
optdepends=('qt5-charts: User Feedback console'
            'qt5-declarative: QML bindings'
            'qt5-svg: User Feedback console')
conflicts=('kuserfeedback<5.245')
replaces=('kuserfeedback<5.245')
makedepends=(clang
             extra-cmake-modules
             qt5-charts
             qt5-declarative
             qt5-svg
             qt5-tools)
source=(https://download.kde.org/stable/$_name/$_name-$pkgver.tar.xz{,.sig})
sha256sums=('252308b822dd4690ea85ab1688c9b0da5512978ac6b435f77a5979fc1d2ffd13'
            'SKIP')
validpgpkeys=(E0A3EB202F8E57528E13E72FD7574483BB57B18D) # Jonathan Esk-Riddell <jr@jriddell.org>

build() {
  cmake -B build -S $_name-$pkgver \
    -DCMAKE_BUILD_TYPE=Release  \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
