# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_name=kwallet
pkgname=${_name}5
pkgver=5.113.0
pkgrel=3.1
pkgdesc='Secure and unified container for user passwords'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(knotifications5 kservice5 kconfigwidgets5 gpgme qca-qt5)
makedepends=(extra-cmake-modules kdoctools5 boost doxygen qt5-tools qt5-doc)
optdepends=('kwalletmanager: Configuration GUI')
conflicts=("$_name<5.111")
replaces=("$_name<5.111")
provides=(org.freedesktop.secrets)
replaces+=(kwallet-secrets)
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$_name-$pkgver.tar.xz{,.sig})
sha256sums=('96535263b8b57467044aa41ae10a74c79e24e02b40b4abff4a41a65b5869082b'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $_name-$pkgver \
    -DCMAKE_BUILD_TYPE=Release  \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
