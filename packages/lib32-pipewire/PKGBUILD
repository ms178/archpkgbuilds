# Maintainer: David Runge <dvzrv@archlinux.org>
# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgbase=lib32-pipewire
pkgname=(lib32-pipewire lib32-pipewire-jack lib32-pipewire-v4l2)
_commit=64a3edf89ec07957107dff984f053f41ded443fe  # tags/0.3.58
pkgver=0.3.58
pkgrel=3.1
epoch=1
pkgdesc="Low-latency audio/video router and processor - 32-bit client library"
url="https://pipewire.org"
arch=(x86_64)
license=(MIT)
makedepends=(git lib32-alsa-lib lib32-dbus lib32-systemd meson)
options=(strip)
source=(git+https://gitlab.freedesktop.org/pipewire/pipewire.git#commit=$_commit)
sha256sums=('SKIP')

pkgver() {
  cd pipewire
  git describe --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd pipewire
}

build() {
  local meson_options=(
    --libdir /usr/lib32
    -D bluez5-codec-lc3plus=disabled
    -D examples=disabled
    -D man=disabled
    -D tests=disabled
    -D gstreamer=disabled
    -D gstreamer-device-provider=disabled
    -D systemd-user-service=disabled
    -D bluez5=disabled
    -D jack=disabled
    -D jack-devel=true
    -D libjack-path=/usr/lib32
    -D v4l2=disabled
    -D b_ndebug=true
    -D b_pie=false
    -D c_std=gnu18
    -D cpp_std=gnu++17
    -D b_lto=true
    --buildtype=release
    --wrap-mode=nofallback
    -D libcamera=disabled
    -D libcanberra=disabled
    -D x11=disabled
    -D x11-xfixes=disabled
    -D pw-cat=disabled
    -D udevrulesdir=/usr/lib/udev/rules.d
    -D sdl2=disabled
    -D sndfile=disabled
    -D libpulse=disabled
    -D roc=disabled
    -D avahi=disabled
    -D echo-cancel-webrtc=disabled
    -D libusb=disabled
    -D session-managers=[]
    -D raop=disabled
    -D lv2=disabled
  )

  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  arch-meson pipewire build "${meson_options[@]}"
  meson compile -C build
}

#check() {
#  meson test -C build --print-errorlogs
#}

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

_ver=${pkgver:0:3}

package_lib32-pipewire() {
  depends=(pipewire lib32-systemd libdbus-1.so libasound.so)
  optdepends=('lib32-pipewire-jack: JACK support'
              'lib32-pipewire-v4l2: V4L2 interceptor')
  provides=(libpipewire-$_ver.so)

  meson install -C build --destdir "$pkgdir"

  ( cd "$pkgdir"

    _pick jack usr/lib32/libjack*
    _pick jack usr/lib32/pkgconfig/jack.pc

    _pick v4l2 usr/lib32/pipewire-$_ver/v4l2

    rm -r usr/{bin,include,lib,share}
  )

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_lib32-pipewire-jack() {
  pkgdesc+=" - JACK support"
  license+=(GPL2)  # libjackserver
  depends=(pipewire-jack lib32-pipewire libpipewire-$_ver.so)
  conflicts=(lib32-jack lib32-jack2)
  provides=(lib32-jack libjack.so libjackserver.so libjacknet.so)

  mv jack/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}

package_lib32-pipewire-v4l2() {
  pkgdesc+=" - V4L2 interceptor"
  depends=(pipewire-v4l2 lib32-pipewire libpipewire-$_ver.so)

  mv v4l2/* "$pkgdir"

  install -Dt "$pkgdir/usr/share/licenses/$pkgname" -m644 pipewire/COPYING
}
