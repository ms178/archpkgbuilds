# Maintainer: Zanny <lordzanny@gmail.com>
# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=kcodecs-git
_name=${pkgname%-git}
pkgver=5.95.0_r419.gaa3d702
pkgrel=1
pkgdesc='Plugins allowing Qt applications to access further types of images'
arch=(x86_64)
url='https://projects.kde.org/projects/frameworks/kcodecs'
license=(LGPL)
depends=(qt6-base)
makedepends=(extra-cmake-modules gperf git qt6-tools)
groups=(kf6)
conflicts=(kcodecs)
provides=(kcodecs)
source=("git+https://invent.kde.org/frameworks/$_name.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  _ver="$(grep -m1 'set(KF5\?_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_name \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
