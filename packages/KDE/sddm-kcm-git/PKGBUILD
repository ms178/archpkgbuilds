# Maintainer: Peter Mattern <pmattern at arcor dot de>
# Contributor: Bhushan Shah
# Contributor: Antonio Rojas

_pkgname=sddm-kcm
pkgname=$_pkgname-git
pkgver=5.25.80_r670.g08ecf2b
pkgrel=1
pkgdesc='KDE Config Module for SDDM'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/kdereview/sddm-kcm/'
license=('GPL')
depends=('kcmutils-git')
makedepends=('git' 'extra-cmake-modules-git' 'knewstuff-git')
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=("git+https://invent.kde.org/plasma/sddm-kcm.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  _ver="$(grep -m1 'set(PROJECT_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  mkdir -p build && cd build
  cmake ${srcdir}/${_pkgname} \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DQT_MAJOR_VERSION=6 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
