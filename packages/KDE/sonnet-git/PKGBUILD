# Maintainer: zan <zan@420blaze.it>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>

pkgname=sonnet-git
_name=${pkgname%-git}
pkgver=v5.94.0.693.18dc4a9
pkgrel=1
pkgdesc='Spelling framework for Qt'
arch=(x86_64)
url='https://invent.kde.org/frameworks/sonnet'
license=(LGPL)
depends=(qt6-base)
makedepends=(git extra-cmake-modules-git qt6-declarative qt6-tools hunspell aspell hspell libvoikko doxygen)
conflicts=(${pkgname%-git})
provides=(${pkgname%-git})
optdepends=('hunspell: spell checking via hunspell' 'aspell: spell checking via aspell' 
            'hspell: spell checking for Hebrew' 'libvoikko: Finnish support via Voikko'
            'qt6-declarative: QML bindings')
groups=(kf6-git)
source=("git+https://github.com/KDE/${pkgname%-git}.git")
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  _ver="$(grep -m1 'set(KF_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S ${pkgname%-git} \
    -DQT_MAJOR_VERSION=6 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
