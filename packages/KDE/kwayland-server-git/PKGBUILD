# Maintainer: zan <zan@420blaze.it>

pkgname=kwayland-server-git
_name=${pkgname%-git}
pkgver=5.24.80_r1480.g7f15f36c
pkgrel=1
pkgdesc='Wayland Server Components built on KDE Frameworks '
arch=(x86_64)
url='https://invent.kde.org/plasma/kwayland-server'
license=(LGPL)
depends=(qt5-base)
makedepends=(git extra-cmake-modules-git plasma-wayland-protocols wayland-protocols kwayland doxygen qt5-tools)
conflicts=(kwayland-server)
provides=(kwayland-server)
groups=(plasma)
source=("git+https://invent.kde.org/plasma/$_name.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  _ver="$(grep -m1 'set(PROJECT_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_name \
            -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}




