# Maintainer: zan <zan@420blaze.it>
# Contributor: Antonio Rojas < nqn1976 @ gmail.com >

pkgname=milou-git
_name=${pkgname%-git}
pkgver=5.24.80_r769.gbc9b438
pkgrel=1
pkgdesc="A dedicated search application built on top of Baloo"
arch=(x86_64)
url='https://invent.kde.org/plasma/milou'
license=(LGPL)
provides=(milou)
conflicts=(milou)
groups=(plasma)
depends=(krunner)
makedepends=('extra-cmake-modules' 'git' 'kdoctools')
source=("git+https://invent.kde.org/plasma/$_name.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  _ver="$(grep -m1 'set(PROJECT_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_name \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
