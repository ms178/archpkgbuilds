# Maintainer: zan <zan@420blaze.it>
# Contributor Andrea Scarpino <andrea@archlinux.org>

pkgname=solid-git
_name=${pkgname%-git}
pkgver=5.102.0_r831.g4b07132
pkgrel=1
pkgdesc='Hardware integration and detection'
arch=(x86_64)
url='https://projects.kde.org/projects/frameworks/solid'
license=(LGPL)
depends=(qt6-declarative media-player-info udisks2 upower)
makedepends=(extra-cmake-modules git qt6-tools)
groups=(kf5)
conflicts=(solid)
provides=(solid)
source=("git+https://invent.kde.org/frameworks/solid.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  _ver="$(grep -m1 'set(KF6\?_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_name \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
