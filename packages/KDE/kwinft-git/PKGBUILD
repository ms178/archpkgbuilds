# Maintainer: Christoph Haag <christoph.haag@collabora.com>
# Author: Roman Gilg <subdiff@gmail.com>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_pkgname=kwin
pkgname=kwinft-git
pkgver=5.25.0.beta.0.r238.g8ee06f3b2
pkgrel=1
pkgdesc='drop-in replacement for KWin with additional libwayland wrapping Qt/C++ library Wrapland'
arch=(x86_64)
url="https://gitlab.com/kwinft/kwinft"
license=(LGPL)
depends=(xcb-util-cursor plasma-framework kcmutils kio breeze kinit qt5-sensors qt5-script wrapland disman kdisplay kscreenlocker pipewire libqaccessibilityclient qt5-sensors xorg-server-xwayland qt5-quickcontrols wlroots-git)
makedepends=(git extra-cmake-modules qt5-tools kdoctools)
optdepends=('qt5-virtualkeyboard: virtual keyboard support for kwin-wayland')
provides=("$_pkgname=${pkgver}" "kwinft=${pkgver}")
conflicts=("$_pkgname" "kwinft")
source=("$_pkgname::git+https://gitlab.com/kwinft/kwinft.git" 186.patch 209.patch 225.patch
)
sha256sums=('SKIP')
install=kwinft.install

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/^kwinft\@//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir -p "$srcdir"/build
    local src
    for src in "${source[@]}"; do
        src="${src%%::*}"
        src="${src##*/}"
        [[ $src = *.patch ]] || continue
        echo "Applying patch $src..."
        patch --directory="kwin" --forward --strip=1 < "../$src"
    done
}

build() {
  cd "$srcdir"/build
  cmake "$srcdir"/$_pkgname \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_INSTALL_LIBEXECDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  make
}

package() {
  cd "$srcdir"/build
  make DESTDIR="$pkgdir" install
}
