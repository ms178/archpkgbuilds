# Maintainer : Daniel Bermond <dbermond@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>

pkgname=kde-gtk-config-git
pkgver=5.25.80_r938.gda46494
pkgrel=1
pkgdesc='GTK2 and GTK3 Configurator for KDE (git version)'
arch=('x86_64')
url='https://www.kde.org/workspaces/plasmadesktop/'
license=('LGPL')
depends=('qt6-svg'  'kdecoration'  'kconfigwidgets'  'kdbusaddons')
optdepends=('gtk2: GTK2 apps support'
            'gtk3: GTK3 apps support'
            'xsettingsd: apply settings to GTK applications on the fly')
makedepends=('git' 'extra-cmake-modules' 'gtk2' 'gtk3' 'sassc')
conflicts=('kde-gtk-config')
provides=('kde-gtk-config')
source=('git+https://anongit.kde.org/kde-gtk-config.git')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  _ver="$(grep -m1 'set(PROJECT_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
    cmake -B build -S kde-gtk-config \
        -DCMAKE_INSTALL_PREFIX='/usr' \
        -DLIBEXEC_INSTALL_DIR='lib' \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF \
        -Wno-dev
    make -C build
}

#check() {
#    make -C build test
#}

package() {
    make -C build DESTDIR="$pkgdir" install
}
