# Maintainer: Andrea Scarpino <andrea@archlinux.org>

pkgname=threadweaver-git
pkgver=5.95.0_r539.g557d9bd
pkgrel=1
pkgdesc='ThreadWeaver'
arch=('i686' 'x86_64')
url='https://projects.kde.org/projects/frameworks/threadweaver'
license=('LGPL')
depends=('qt6-base')
makedepends=('extra-cmake-modules-git' 'git')
groups=('kf6')
conflicts=(threadweaver)
provides=(threadweaver)
source=('git+https://invent.kde.org/frameworks/threadweaver.git')
md5sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  _ver="$(grep -m1 'set(KF6\?_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../threadweaver \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DKDE_INSTALL_USE_QT_SYS_PATHS=ON \
    -DQT_MAJOR_VERSION=6 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  make
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
