# Maintainer: zan <zan@nie.rs>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=baloo-git
_name=${pkgname%-git}
pkgver=5.95.0_r3049.geaa4dbdb
pkgrel=1
pkgdesc="A framework for searching and managing metadata"
arch=(x86_64)
url='https://invent.kde.org/frameworks/baloo'
license=(LGPL)
depends=(lmdb kfilemetadata kidletime kio)
makedepends=(extra-cmake-modules git kdoctools python)
groups=(kf5)
provides=(baloo)
conflicts=(baloo)
source=("git+https://invent.kde.org/frameworks/$_name.git")
md5sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  _ver="$(grep -m1 'set(KF6\?_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
  cmake -B build -S $_name \
          -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
