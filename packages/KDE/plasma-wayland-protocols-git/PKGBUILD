# Maintainer: Martin Stolpe <martin.stolpe[at]gmail{dot}com>
# Contributor Antonio Rojas <arojas [at] archlinux.org>

_pkgname=plasma-wayland-protocols
pkgname=plasma-wayland-protocols-git
pkgver=v1.7.0.r1.g26fb07b
pkgrel=1
pkgdesc="Plasma Specific Protocols for Wayland"
arch=(any)
url="https://kde.org/plasma-desktop"
license=(LGPL)
depends=()
makedepends=(qt6-base extra-cmake-modules git)
source=("git+https://invent.kde.org/libraries/${_pkgname}")
sha256sums=('SKIP')
provides=(plasma-wayland-protocols)
conflicts=(plasma-wayland-protocols)

pkgver() {
  cd plasma-wayland-protocols
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S ${_pkgname} \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
