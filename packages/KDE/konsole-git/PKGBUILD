# Maintainer: katt <magunasu.b97@gmail.com>
# Contributor: Felix Yan <felixonmars@archlinux.org>
# Contributor: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgname=konsole-git
pkgver=21.11.90.r465.g4276bd33e
pkgrel=1
arch=(x86_64)
url=https://kde.org/applications/system/konsole
pkgdesc="KDE's terminal emulator"
license=(GPL LGPL FDL)
groups=(kde-applications kde-utilities)
depends=(knotifyconfig kpty kparts knewstuff)
makedepends=(extra-cmake-modules kdoctools git)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
optdepends=('keditbookmarks: to manage bookmarks')
source=(git+https://invent.kde.org/utilities/konsole.git)
sha256sums=('SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7 # Albert Astals Cid <aacid@kde.org>
    F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87           # Christoph Feck <cfeck@kde.org>
    D81C0CB38EB725EF6691C385BB463350D6EF31EF)          # Heiko Becker <heiko.becker@kde.org>

pkgver() {
  cd konsole
  _major_ver="$(grep -m1 'set *(RELEASE_SERVICE_VERSION_MAJOR' CMakeLists.txt | cut -d '"' -f2)"
  _minor_ver="$(grep -m1 'set *(RELEASE_SERVICE_VERSION_MINOR' CMakeLists.txt | cut -d '"' -f2)"
  _micro_ver="$(grep -m1 'set *(RELEASE_SERVICE_VERSION_MICRO' CMakeLists.txt | cut -d '"' -f2)"
  echo "${_major_ver}.${_minor_ver}.${_micro_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

build() {
    cmake -B build -S "${pkgname%-git}" \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
    cmake --build build
}

package() {
    DESTDIR="$pkgdir" cmake --install build
}
