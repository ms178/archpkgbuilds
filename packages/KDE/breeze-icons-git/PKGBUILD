# Maintainer: Gustavo Alvarez <sl1pkn07@gmail.com>
# Contributor: FadeMind <fademind@gmail.com>

pkgname=breeze-icons-git
pkgver=5.95.0_r1896.gcaf9b8e0
pkgrel=1
pkgdesc="Breeze icon themes for KDE Plasma. (GIT version)"
arch=('any')
url='https://quickgit.kde.org/?p=breeze-icons.git'
license=('LGPL')
groups=('kf5')
makedepends=('extra-cmake-modules'
             'git'
             'qt5-base'
             )
provides=('breeze-icons')
conflicts=('breeze-icons')
source=('git+https://invent.kde.org/frameworks/breeze-icons.git')
sha256sums=('SKIP')

pkgver() {
  cd ${pkgname%-git}
  _ver="$(grep -m1 'set(KF5\?_VERSION' CMakeLists.txt | cut -d '"' -f2 | tr - .)"
  echo "${_ver}_r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../breeze-icons \
    -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF \
    -DBINARY_ICONS_RESOURCE=ON

  make
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}
