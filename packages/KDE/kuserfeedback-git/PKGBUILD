# Maintainer: Martin Stolpe <martin dot stolpe at gmail dot com>
# Contributor Antonio Rojas <arojas@archlinux.org>

_pkgname=kuserfeedback
pkgname=kuserfeedback-git
pkgver=v1.2.0.r5.gb55462e
pkgrel=1
pkgdesc="Framework for collecting user feedback for applications via telemetry and surveys"
arch=(x86_64)
url="https://kde.org/products/frameworks/"
license=(GPL)
depends=(qt6-base)
makedepends=(extra-cmake-modules-git qt6-tools clang qt6-charts qt6-svg qt6-declarative git)
optdepends=('qt6-declarative: QML bindings' 'qt6-charts: User Feedback console' 'qt6-svg: User Feedback console')
conflicts=(kuserfeedback)
provides=(kuserfeedback)
source=("git+https://invent.kde.org/libraries/$_pkgname.git")
sha256sums=('SKIP')

pkgver() {
  cd kuserfeedback
  git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cmake -B build -S $_pkgname \
    -DQT_MAJOR_VERSION=6 \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DENABLE_DOCS=OFF \
    -DBUILD_QCH=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
