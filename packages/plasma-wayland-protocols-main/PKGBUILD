# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=plasma-wayland-protocols
pkgver=1.12.0
pkgrel=3.1
pkgdesc='Plasma Specific Protocols for Wayland'
arch=(any)
url='https://kde.org/plasma-desktop'
license=(LGPL)
depends=()
makedepends=(extra-cmake-modules
             qt6-base)
source=(https://download.kde.org/stable/$pkgname/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('1483bfd279cb913c83579b5d71c58f9958764f9ba4303b3647e1007cb70d4f9e'
            'SKIP')
validpgpkeys=(CA1B4E348D22755A67EF4E1A6A4F2C4CDC8BC77F  # Aleix Pol Gonzalez <aleixpol@kde.org>
              EC5516B514543DA145169FD12276F09AF4A790F4) # Vlad Zahorodnii <vlad.zahorodnii@kde.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release  \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
