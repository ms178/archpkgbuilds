# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

pkgbase=libkdcraw
pkgname=(libkdcraw)
pkgver=23.08.3
pkgrel=4.1
pkgdesc='A C++ interface used to decode RAW picture'
url='https://www.kde.org/'
arch=(x86_64)
license=(GPL LGPL FDL)
depends=(qt5-base libraw)
makedepends=(extra-cmake-modules)
conflicts=('libkdcraw<24')
replaces=('libkdcraw<24')
source=(https://download.kde.org/stable/release-service/$pkgver/src/$pkgbase-$pkgver.tar.xz{,.sig})
sha256sums=('5406fbbbaded4af8001581dbdb0b85fd9e8ba88df162212a4683e2e9206df32f'
            'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7  # Albert Astals Cid <aacid@kde.org>
              F23275E4BF10AFC1DF6914A6DBD2CE893E2D1C87  # Christoph Feck <cfeck@kde.org>
              D81C0CB38EB725EF6691C385BB463350D6EF31EF) # Heiko Becker <heiko.becker@kde.org>

build() {
  cmake -B build -S $pkgbase-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=20 \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
