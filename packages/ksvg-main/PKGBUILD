# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=ksvg
pkgver=5.113.0
pkgrel=3.1
pkgdesc='Components for handling SVGs'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(gcc-libs
         glibc
         karchive
         kconfig
         kcoreaddons
         kguiaddons
         qt5-base
         qt5-svg)
makedepends=(doxygen
             extra-cmake-modules
             qt5-declarative
             qt5-doc
             qt5-tools)
optdepends=('kirigami: QML bindings'
            'qt6-declarative: QML bindings')
groups=(kf6)
source=(https://download.kde.org/stable/frameworks/$pkgver/$pkgname-$pkgver.tar.xz{,.sig})
sha256sums=('fc5fd0420638afdfe37e5cc2e4b2942b82cfd6808990b12e6056913ad39de391'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB  # David Faure <faure@kde.org>
              E0A3EB202F8E57528E13E72FD7574483BB57B18D) # Jonathan Esk-Riddell <jr@jriddell.org>

build() {
  cmake -B build -S $pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release  \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
