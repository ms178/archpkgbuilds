# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=pciutils-git
pkgver=3.12.0.r3.g8c140be
pkgrel=1
pkgdesc="Programs for inspecting and manipulating configuration of PCI devices"
arch=('i686' 'x86_64')
url="https://mj.ucw.cz/sw/pciutils/"
license=('GPL2')
depends=('glibc' 'hwdata' 'kmod')
makedepends=('git' 'llvm' 'clang' 'lld')
provides=("pciutils=$pkgver")
conflicts=('pciutils')
options=('staticlibs')
source=("git+https://git.kernel.org/pub/scm/utils/pciutils/pciutils.git")
sha256sums=('SKIP')


pkgver() {
  cd "pciutils"

  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "pciutils"

  make \
    OPT="$CFLAGS" \
    SHARED=no \
    ZLIB=no \
    CC=clang \
    LD=ld.lld \
    AR=llvm-ar \
    RANLIB=llvm-ranlib
  cp "lib/libpci.a" "$srcdir"

  make clean
  make \
    OPT="$CFLAGS" \
    SHARED=yes \
    ZLIB=no \
    CC=clang \
    LD=ld.lld \
    AR=llvm-ar \
    RANLIB=llvm-ranlib
}

package() {
  cd "pciutils"

  make \
    DESTDIR="$pkgdir" \
    PREFIX="/usr" \
    SBINDIR="/usr/bin" \
    SHAREDIR="/usr/share/hwdata" \
    SHARED=yes \
    ZLIB=no \
    CC=clang \
    LD=ld.lld \
    AR=llvm-ar \
    RANLIB=llvm-ranlib \
    install \
    install-lib

  install -Dm644 "$srcdir/libpci.a" -t "$pkgdir/usr/lib"

  # supplied by hwids package
  rm -rf "$pkgdir/usr"/{bin/update-pciids,share/{hwdata,man/man8/update-pciids.8}}
}
