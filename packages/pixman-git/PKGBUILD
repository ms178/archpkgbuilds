# Maintainer:  Vincent Grande <shoober420@gmail.com>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=pixman-git
pkgver=0.43.4.r17.g0cb4fbe
pkgrel=1
pkgdesc="The pixel-manipulation library for X and cairo"
arch=(x86_64)
url="https://cgit.freedesktop.org/pixman/"
license=('custom')
provides=(pixman libpixman-1.so=0-64)
conflicts=(pixman)
depends=('glibc')
makedepends=('meson' 'libpng')
source=(git+https://gitlab.freedesktop.org/pixman/pixman.git)
sha1sums=('SKIP')

pkgver() {
    cd pixman
      git describe --long --tags | sed 's/^[_a-z]*\([0-9.]*\)[^-]*/\1/;s/\([^-]*-g\)/r\1/;s/-/./g;s/^\.//'
    }

prepare() {
    # although removing _build folder in build() function feels more natural,
    # that interferes with the spirit of makepkg --noextract
    if [  -d _build ]; then
        rm -rf _build
    fi

    local src
    for src in "${source[@]}"; do
        src="${src%%::*}"
        src="${src##*/}"
        [[ $src = *.patch ]] || continue
        echo "Applying patch $src..."
        patch --directory="pixman" --forward --strip=1 < "$src"
    done
}

build() {
  arch-meson pixman _build \
    -D loongson-mmi=disabled \
    -D a64-neon=disabled \
    -D rvv=disabled \
    -D tests=disabled \
    -D tls=enabled \
    -D b_ndebug=true \
    -D b_pie=false \
    -D c_std=gnu18 \
    -D cpp_std=gnu++2a \
      --buildtype=release \
      --wrap-mode=nofallback \
    -D vmx=disabled \
    -D arm-simd=disabled \
    -D neon=disabled \
    -D mips-dspr2=disabled \
    -D mmx=disabled \
    -D sse2=enabled \
    -D ssse3=enabled \
    -D gtk=disabled
  ninja $NINJAFLAGS -C _build
}

#check() {
#  meson test -C build
#}

package() {
  DESTDIR="$pkgdir" meson install -C _build
  install -Dt "$pkgdir/usr/share/licenses/pixman" -m644 pixman/COPYING
}
