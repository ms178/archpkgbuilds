# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>

_name=knotifications
pkgname=${_name}5
pkgver=5.114.0
pkgrel=3.1
pkgdesc='Abstraction for system notifications'
arch=(x86_64)
url='https://community.kde.org/Frameworks'
license=(LGPL)
depends=(libcanberra libdbusmenu-qt5 kwindowsystem5 kconfig5 kcoreaddons5 qt5-speech libxtst)
makedepends=(extra-cmake-modules qt5-tools qt5-doc doxygen qt5-declarative)
optdepends=('qt5-declarative: QML bindings')
conflicts=("$_name<5.111")
replaces=("$_name<5.111")
groups=(kf5)
source=(https://download.kde.org/stable/frameworks/${pkgver%.*}/$_name-$pkgver.tar.xz{,.sig})
sha256sums=('3dd0f9a0260064b042309ee0ad0c54d3d9dc64166421f3cf1e7044cd4570f7fb'
            'SKIP')
validpgpkeys=(53E6B47B45CEA3E0D5B7457758D0EE648A48B3BB) # David Faure <faure@kde.org>

build() {
  cmake -B build -S $_name-$pkgver \
    -DCMAKE_BUILD_TYPE=Release  \
    -DBUILD_TESTING=OFF \
    -DBUILD_QCH=OFF \
    -DCMAKE_C_STANDARD=23 \
    -DCMAKE_CXX_STANDARD=23
  cmake --build build
}

package() {
  DESTDIR="$pkgdir" cmake --install build
}
