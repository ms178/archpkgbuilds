# Maintainer: Piotr GÃ³rski <lucjan.lucjanov@gmail.com>
# Contributor: Oleksandr Natalenko <oleksandr@natalenko.name>
# Contributor: Damian N. <nycko123 at gmail>

pkgname=uksmd-git
gitname=uksmd
pkgver=0.0.0.r49.g7a21591
pkgrel=3.1
epoch=1
pkgdesc="Userspace KSM helper daemon"
url="https://codeberg.org/sirlucjan/uksmd"
license=('GPL3')
arch=('x86_64')
depends=('procps-ng' 'libcap-ng')
makedepends=('meson' 'git')
conflicts=('uksmd')
provides=("uksmd=${pkgver}")
#source=('git+https://codeberg.org/pf-kernel/uksmd')
source=('git+https://codeberg.org/sirlucjan/uksmd')
sha256sums=('SKIP')

pkgver() {
  cd $gitname
  _ver="$(cat meson.build | grep -m1 'version' | grep -o "[[:digit:]]*" | paste -sd'.')"
  printf "${_ver}.r$(git rev-list --count HEAD).g$(git rev-parse --short HEAD)"
}

prepare() {
 cd $gitname

 local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    echo "Applying patch $src..."
    patch -Np1 < "../$src"
  done
}

build() {
  cd $gitname
  arch-meson . build \
      -D b_ndebug=true \
      -D b_pie=false \
      -D c_std=gnu2x \
      -D cpp_std=gnu++2a \
      -D b_lto=true \
      --buildtype=release \
      --wrap-mode=nofallback
  meson compile -C build
}

package() {
  depends+=(UKSMD-BUILTIN)

  cd $gitname
  meson install -C build --destdir "${pkgdir}"
}
