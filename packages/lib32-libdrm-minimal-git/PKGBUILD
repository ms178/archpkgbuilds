# Maintainer : Lone_Wolf <lonewolf@klaas-de-kat.nl>
# Contributor : Eric Engestrom <eric@engestrom.ch>
# Contributor: Laurent Carlier <lordheavym@gmail.com>
# Contributor : Jan de Groot <jgc@archlinux.org>

pkgname=lib32-libdrm-minimal-git
pkgver=2.4.120.r29.g37265ab0
pkgrel=1
pkgdesc="Userspace interface to kernel DRM services, git 32-bit version"
arch=(x86_64)
license=('custom')
depends=('libdrm-git' 'lib32-libpciaccess' 'lib32-gcc-libs')
makedepends=('python-docutils' 'meson' 'git' 'ninja')
checkdepends=('lib32-cairo')
url="http://dri.freedesktop.org/"
provides=('lib32-libdrm')
conflicts=('lib32-libdrm')

source=('libdrm::git+https://gitlab.freedesktop.org/mesa/drm.git'
        COPYING)
sha256sums=('SKIP'
            '9631d4f694952e3e6ae5a05534c2e93e994e47d3413677a3a00e45c8cef6db93')

          
pkgver() {
    cd libdrm
    git describe --long --tags --abbrev=8 | sed 's/^libdrm-//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    if [  -d _build ]; then
        rm -rf _build
    fi
}

build() {
  export CC="clang -m32"
  export CXX="clang++ -m32"
  export ASMFLAGS="-m32"
    export PKG_CONFIG=/usr/bin/i686-pc-linux-gnu-pkg-config
    meson setup libdrm _build \
        --prefix /usr \
        --libdir lib32 \
        -D python.bytecompile=2 \
    	-D b_ndebug=true \
    	-D b_pie=false \
    	-D c_std=gnu18 \
    	-D cpp_std=gnu++2a \
    	-D b_lto=true \
    	--unity=on \
      	--buildtype=release \
      	--wrap-mode=nofallback \
    	-D udev=false \
    	-D valgrind=disabled \
    	-D freedreno=disabled \
    	-D vc4=disabled \
    	-D vmwgfx=disabled \
        -D amdgpu=enabled \
        -D radeon=enabled \
        -D intel=disabled \
        -D nouveau=disabled \
        -D man-pages=disabled

    meson configure _build --no-pager
    meson compile -C _build
}

#check() {
   # '-t 10' is needed for the 'threaded' test, which uses the default meson
   # test timeout of 30 seconds. This is too short for many systems. It can be
   # removed if upstream fixes the issue.
#    meson test -C _build -t 10
#}

package() {
    DESTDIR="$pkgdir" meson install -C _build
  
    # remove files already provided by libdrm-git
    rm -rf "$pkgdir"/usr/{include,share,bin}
  
    install -Dt "$pkgdir/usr/share/licenses/$pkgname"  -m644 COPYING
}
