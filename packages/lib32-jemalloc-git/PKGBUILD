# Maintainer: Chocobo1 <chocobo1 AT archlinux DOT net>

pkgname=lib32-jemalloc-git
pkgver=5.3.0.r159.g373884ab
pkgrel=1
pkgdesc="General purpose malloc implementation"
arch=('i686' 'x86_64')
url="https://jemalloc.net/"
license=('BSD')
depends=('lib32-glibc' 'sh')
makedepends=('git' 'docbook-xsl')
optdepends=('perl: for running jeprof')
provides=("lib32-jemalloc=$pkgver")
source=("git+https://github.com/jemalloc/jemalloc.git")
sha256sums=('SKIP')


pkgver() {
  cd "jemalloc"

  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "jemalloc"

  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  export LDFLAGS+=' -m32'

  _xslroot="/usr/share/xml/docbook/xsl-stylesheets-$(pacman -Q docbook-xsl | cut -d ' ' -f 2 | cut -d '-' -f 1)"
  autoreconf -vfi
  ./configure \
    --prefix="/usr" \
    --libdir=/usr/lib32 \
    --disable-debug \
    --disable-log \
    --disable-doc \
    --with-xslroot="$_xslroot"
  make
}

package() {
  cd "jemalloc"
  make DESTDIR="${pkgdir}" install
  chmod 644 "${pkgdir}/usr/lib32/libjemalloc_pic.a"
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}
