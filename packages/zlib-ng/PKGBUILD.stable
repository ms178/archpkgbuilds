pkgname=(
  zlib-ng
  zlib-ng-compat
)
pkgver=2.2.3
pkgrel=3.1
_zlibver=1.3.1 # Keep this consistent with the actual zlib version you're replacing
pkgdesc='zlib replacement with optimizations for next generation systems'
url='https://github.com/zlib-ng/zlib-ng'
arch=('x86_64')
license=('custom:zlib')
depends=(
  glibc
)
makedepends=(
  cmake
  ninja
  git
)
source=(git+https://github.com/zlib-ng/zlib-ng#tag=${pkgver})
sha256sums=('6867f8f1e57d3e8e4158292a839fa29967c58849aabeba2199e3d95b346d675f')
b2sums=('5011d94bff127ffbd009fea15b4aea036e18fc241d37972856f68ed474a72bd0e7f75cff74e1a6c9e6c0132ca436ad7a7aa246dcc29747bd7dcedb5f8e35cbca')

build() {
  cd "zlib-ng"

  export CFLAGS+=" -fno-semantic-interposition"
  export CXXFLAGS+=" -fno-semantic-interposition"

  local _options=(
    -G Ninja
    -DCMAKE_BUILD_TYPE=Release
    -DCMAKE_C_STANDARD=11
    -DCMAKE_CXX_STANDARD=23
    -DCMAKE_INSTALL_PREFIX=/usr
    -DCMAKE_INSTALL_LIBDIR=lib
    -Wno-dev
    -DWITH_GTEST=OFF
  )

  msg2 "Building zlib-ng"
  cmake -B build \
    "${_options[@]}"
  cmake --build build

  msg2 "Building zlib-ng-compat"
  cmake -B build-compat \
    "${_options[@]}" \
    -DZLIB_COMPAT=ON
  cmake --build build-compat
}

check() {
  cd "zlib-ng"
  msg2 "Checking zlib-ng"
  ctest --output-on-failure --test-dir build
  msg2 "Checking zlib-ng-compat"
  ctest --output-on-failure --test-dir build-compat
}

package_zlib-ng() {
  pkgdesc='zlib replacement with optimizations for next generation systems'
  provides=("libz-ng.so")

  cd "zlib-ng"
  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname[0]}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname[0]}"
}

package_zlib-ng-compat() {
  pkgdesc='zlib replacement with optimizations for next generation systems (zlib compat)'
  provides=("zlib=1:${_zlibver}" "libz.so")
  depends=('zlib-ng')
  conflicts=('zlib')
  replaces=('zlib')
  options=('staticlibs')

  cd "zlib-ng"
  DESTDIR="${pkgdir}" cmake --install build-compat
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname[0]}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname[0]}"
}

# vim: ts=2 sw=2 et:
