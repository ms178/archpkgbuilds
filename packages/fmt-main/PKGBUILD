pkgbase=fmt
pkgname=(
  fmt
)
pkgver=11.1.4
pkgrel=1
pkgdesc='Open-source formatting library for C++'
arch=(x86_64)
url=https://fmt.dev
license=(MIT)
makedepends=(
  cmake
  doxygen
  git
  mkdocs
  mkdocs-material
  mkdocstrings
  ninja
  npm
  python-pymdown-extensions
  python-regex
  clang
  llvm
  llvm-libs
)
_tag=123913715afeb8a437e6388b4473fcc4753e1c9a
source=("git+https://github.com/fmtlib/fmt.git#tag=$_tag")
b2sums=('72c7027f23292ccd5bdcc3b797547a52b9a3c59e98485a7b2bfcefcd482fb2efb761966947f42eb61bdb6f195ebbede80f18ad603b8ccc0d1a744575433892f6')

_common_instrument_flags=" -g3 -fno-omit-frame-pointer -mllvm -vp-counters-per-site=10 -mllvm -runtime-counter-relocation -mllvm -enable-value-profiling"

pkgver() {
  cd fmt
  git describe --tags
}

prepare() {
  mkdir -p "$srcdir/pgo-profiles/standard" "$srcdir/pgo-profiles/context-sensitive"
}

build() {
  cmake -S fmt -B build-pgo-standard -G Ninja \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_CXX_COMPILER=clang++ \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${_common_instrument_flags} -fprofile-generate=$srcdir/pgo-profiles/standard" \
    -DCMAKE_EXE_LINKER_FLAGS="${LDFLAGS}" \
    -Wno-dev
  cmake --build build-pgo-standard

  cmake --build build-pgo-standard --target test

  llvm-profdata merge -output="$srcdir/pgo-profiles/standard.profdata" "$srcdir/pgo-profiles/standard"/*.profraw

  cmake -S fmt -B build-pgo-cs -G Ninja \
    -DCMAKE_BUILD_TYPE=Debug \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_CXX_COMPILER=clang++ \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} ${_common_instrument_flags} -fprofile-use=$srcdir/pgo-profiles/standard.profdata -fcs-profile-generate=$srcdir/pgo-profiles/context-sensitive" \
    -DCMAKE_EXE_LINKER_FLAGS="${LDFLAGS}" \
    -Wno-dev
  cmake --build build-pgo-cs

  cmake --build build-pgo-cs --target test

  llvm-profdata merge -output="$srcdir/pgo-profiles/combined.profdata" "$srcdir/pgo-profiles/standard.profdata" "$srcdir/pgo-profiles/context-sensitive"/*.profraw

  cmake -S fmt -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DBUILD_SHARED_LIBS=ON \
    -DCMAKE_CXX_COMPILER=clang++ \
    -DCMAKE_CXX_FLAGS="${CXXFLAGS} -fprofile-use=$srcdir/pgo-profiles/combined.profdata" \
    -DCMAKE_EXE_LINKER_FLAGS="${LDFLAGS}" \
    -Wno-dev
  cmake --build build
}

check() {
  cmake --build build --target test
}

package_fmt() {
  depends=(
    gcc-libs
    glibc
  )
  provides=(libfmt.so libfmt.so=11-64)

  DESTDIR="$pkgdir" cmake --install build --component fmt-core
  install -vDm644 -t "$pkgdir/usr/share/licenses/$pkgname" fmt/LICENSE

  rm -rf "$srcdir/pgo-profiles"
}
