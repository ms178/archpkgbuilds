# Maintainer: MGislv <mgislv at ceppo dot xyz>

pkgname=lib32-zlib-ng-compat-git
pkgver=2.1.6.r82.g93b870fb
pkgrel=1
pkgdesc='Zlib replacement with optimizations for next generation systems'
url='https://github.com/zlib-ng/zlib-ng'
arch=('x86_64' 'i686' 'pentium4' 'armv7h' 'aarch64')
license=('custom')
depends=('lib32-glibc')
makedepends=('git')
conflicts=('lib32-zlib' 'lib32-zlib-ng')
provides=('lib32-zlib' libz.so=1-32)
options=('!emptydirs')
source=("git+$url.git")
md5sums=('SKIP')

pkgver() {
  cd "zlib-ng"

  git describe --long --tags | sed 's/^[_a-z]*\([0-9.]*\)[^-]*/\1/;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
	export CFLAGS+=" -m32"
	export CXXFLAGS+=" -m32"
	export LDFLAGS+=" -m32"
	export ASMFLAGS+=" -m32"
	export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'


	cd zlib-ng
	CFLAGS="$CFLAGS" LDFLAGS="$LDFLAGS" ./configure --prefix=/usr --libdir=/usr/lib32 --zlib-compat
	make
}

check() {
	make -C zlib-ng test
}

package() {
  make install DESTDIR="$pkgdir" -C zlib-ng

  rm -rf "$pkgdir"/usr/{include,share,bin}
  install -vDm 644 zlib-ng/LICENSE.md -t "$pkgdir/usr/share/licenses/$pkgname/"
}
