# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Fabien Dubosson <fabien.dubosson@gmail.com>
# Contributor: Konstantin Gizdov <arch@kge.com>

pkgname=xxhash
pkgver=0.8.3
pkgrel=3.1
pkgdesc='Extremely fast non-cryptographic hash algorithm'
arch=(x86_64)
url=https://cyan4973.github.io/xxHash/
license=(
  GPL2
  BSD
)
depends=(glibc)
makedepends=(git)
provides=(libxxhash.so)
_tag=e626a72bc2321cd320e953a0ccf1584cad60f363
source=(git+https://github.com/Cyan4973/xxHash.git#tag=${_tag})
b2sums=('SKIP')

pkgver() {
  cd xxHash
  git describe --tags | sed 's/^v//'
}

build() {
  make PREFIX=/usr DISPATCH=0 XXH_X86DISPATCH_ALLOW_AVX=1 -C xxHash
}

package() {
  make PREFIX=/usr DISPATCH=0 XXH_X86DISPATCH_ALLOW_AVX=1 DESTDIR="${pkgdir}" -C xxHash install
  install -Dm 644 xxHash/LICENSE -t "${pkgdir}"/usr/share/licenses/xxhash
}

# vim: ts=2 sw=2 et:
