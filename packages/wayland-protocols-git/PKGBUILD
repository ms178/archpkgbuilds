# Maintainer: Gennadiy Mykhailiuta <gmykhailiuta@gmail.com>
# Contributor: Emmanuel Gil Peyrot <emmanuel.peyrot@collabora.com>

_pkgname=wayland-protocols
pkgname="${_pkgname}-git"
pkgver=1.33.r0.g5434607
pkgrel=1
pkgdesc='Wayland protocols that add functionalities not available in the core protocol'
arch=('any')
url='https://wayland.freedesktop.org/'
license=('MIT')
makedepends=('git' 'wayland' 'meson' 'ninja')
conflicts=("${_pkgname}")
provides=("${_pkgname}")

source=("git+https://gitlab.freedesktop.org/wayland/wayland-protocols.git")
sha1sums=('SKIP')

pkgver() {
    cd "$_pkgname"
    git describe --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    mkdir -p "${_pkgname}/build"

        local src
    for src in "${source[@]}"; do
        src="${src%%::*}"
        src="${src##*/}"
        [[ $src = *.patch ]] || continue
        echo "Applying patch $src..."
        patch --directory="${_pkgname}" --forward --strip=1 < "$src"
    done

}

build() {
    meson build "${_pkgname}" --prefix=/usr \
      -D b_ndebug=true \
      -D b_pie=false \
      -D c_std=gnu2x \
      -D cpp_std=gnu++2a \
      --buildtype=release \
      --wrap-mode=nofallback
    ninja -C build
}

check() {
    ninja -C build test
}

package() {
    DESTDIR="$pkgdir" ninja -C build install
    install -Dt "$pkgdir/usr/share/licenses/${_pkgname}" -m 644 "$_pkgname/COPYING"
}
